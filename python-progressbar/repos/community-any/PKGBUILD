# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=4.1.1
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-progressbar"
license=(BSD)
depends=(python python-utils)
makedepends=(python-build python-installer python-setuptools python-sphinx python-wheel)
checkdepends=(python-pytest python-freezegun)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('4df7652584c867589448ea8765ba2da99ce5e4c9cfc27268ff850dfe41efa6342a6a44f19384c5c54ef8c79432db35a755958ed9561cf2d37f98fc661a42099f'
            'SKIP')
b2sums=('f9e73c29aac68f3f12c978e29990382dd2a31f668159f9198c162901b2e4ae1e52943fb0a3259fadb8aa62e45e57c0f867923e064953c6f9ddc5ff2c62a5dea1'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
