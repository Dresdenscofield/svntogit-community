# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ini2toml
pkgver=0.11.2
pkgrel=1
pkgdesc="Automatically conversion of .ini/.cfg files to TOML equivalents"
url="https://github.com/abravalheri/ini2toml"
license=('MPL')
arch=('any')
# The default installation is broken. Adding [full] flavor dependencies here.
depends=('python-configupdater' 'python-packaging' 'python-tomlkit')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-tomli-w' 'python-validate-pyproject')
source=("https://github.com/abravalheri/ini2toml/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('49f61da346ffb7494b5d8f5912171792f1243b2ace8fdf51c5ba30fddfc14b237d142094455a242af6f4021ea787fbcc921f426c2a4f4663ca8a8c251a1600fb')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd ini2toml-$pkgver
  python setup.py build
}

check() {
  cd ini2toml-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  # TODO
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest --deselect tests/test_cli.py::test_auto_formatting
}

package() {
  cd ini2toml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
