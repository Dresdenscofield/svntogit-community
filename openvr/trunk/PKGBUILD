# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Christoph Haag <christoph.haag@collabora.com>

pkgname=openvr
pkgver=1.16.8
pkgrel=2
pkgdesc="API and runtime that allows access to VR hardware from multiple vendors"
arch=(x86_64)
url="https://github.com/ValveSoftware/openvr"
license=(custom)
depends=(jsoncpp libgl)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        openvr-fix-vsprintf_s.patch::${url}/pull/594.patch
        openvr-use-system-jsoncpp.patch::${url}/pull/1178.patch
        openvr-fix-1.6.18-include.patch::${url}/pull/1524.patch
        0001-Add-include-stdarg.h-to-strtools_public.cpp.patch)
sha256sums=('387c98c0540f66595c4594e5f3340a1095dd90e954ff14fd5d89cc849ba32d1b'
            '1d954060f27b16485e398654d6730105abc251d372da03fbba38496db17885ae'
            'f189bca77b58597d48ca674ce3dbe1ea783566bec5d35907166a8e8eb944ce7d'
            '859b8173f373e835c6f4606445e542bf5cb991efdf238eb9d280f9ba07bd5ca7'
            'ccb5672d1ec4ce75c8a15700be17b29f743245db0a015bff4ca8a416c037838b')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -rf thirdparty/jsoncpp
  patch -p1 < ../openvr-fix-vsprintf_s.patch
  patch -p1 < ../openvr-use-system-jsoncpp.patch
  patch -p1 < ../openvr-fix-1.6.18-include.patch
  patch -p1 < ../0001-Add-include-stdarg.h-to-strtools_public.cpp.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED=1 \
    -DUSE_SYSTEM_JSONCPP=True
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
