# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pydrive2
_pkgname="${pkgname#python-}"
pkgver=1.10.1
pkgrel=1
pkgdesc='Google Drive API Python wrapper library (maintained fork of PyDrive)'
arch=('any')
url='https://github.com/iterative/PyDrive2'
license=('Apache')
depends=(
  'python'
  'python-google-api-python-client'
  'python-six'
  'python-oauth2client'
  'python-pyaml'
  'python-pyopenssl'
)
makedepends=('git' 'python-setuptools')
_commit='0547b42833f821230316e7e9d22511cfde004e4b'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
