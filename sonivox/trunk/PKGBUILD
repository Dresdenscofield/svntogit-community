# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sonivox
pkgver=3.6.10.14
pkgrel=1
pkgdesc="Fork of the AOSP 'platform_external_sonivox' to use outside of Android"
arch=(x86_64)
url="https://github.com/pedrolcl/sonivox"
license=(Apache)
depends=(glibc)
makedepends=(cmake gtest)
provides=(libsonivox.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/pedrolcl/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('01b4870e38ca7a1ca6d781a149aa0f37eff045b54c0199e59f54c204983e6c57')
b2sums=('bff3580613ef55608e752531c1dff8cf14ca81e15f36801e929e17873bc8707e0f41b5e2a0a2b64fefb9d49dd0227764667411f299987faea79dd4fe0f9ce5e3')

build() {
  cmake \
    -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
