# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.1.2
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-appdirs' 'python-click' 'python-cryptography' 'python-iosbackup'
         'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests'
         'python-simplejson' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('5c47bcb487c5dbb9c0a3b7c5d81bd15188c172706e851efc7eb1179330ddbf44803ed33b18d6ae8b98ce3342bab18ab35846691b204ab3e146851ff3fc2ac8df')
b2sums=('10f3fedfffcf04537eb78b72434c3f8267f743c0fc22c919ba5492fcef68593f739da8c910c3ac854bfb70efc8e0311eb15ce887b05dca40b369bf93d56149af')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
