# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.15.3
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
optdepends=('python-pytest: for pytest plugin')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('96c0f17c3c2924ddcdafa7880b98b2ffa11f1a201fda53c13aba157df5e82719d0f81c90f01042a2b3f5bd3ce125544a5a817a5139c159f35ca38669fb112a93')
b2sums=('8c7801c0677b075f3008f24792f97b8b5211345652ece0f4759d1cc614cece33597ef7723d39a20828cb7af6b18e1bed2d9f1216e835a95a07ad7c1f639f8a7b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages"
  # disable plugin test until upstream fixes the contents of the sdist tarball: https://github.com/tmux-python/libtmux/issues/441
  pytest -vv tests -k "not test_plugin"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
