# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Renato Garcia <fgarcia.renato@gmail.com>

pkgname=trash-cli
pkgver=0.22.10.20
pkgrel=1
pkgdesc="Command line trashcan (recycle bin) interface"
arch=('any')
url="https://github.com/andreafrancia/trash-cli"
license=('GPL')
depends=('python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-six' 'python-flexmock' 'python-parameterized')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('74f8dabc4bb0da368bbfe847eccbeaf75326d00b8e85849d6c2b7b9044c4fadc')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # don't depend on thirdparty copies of the stdlib
    find tests -type f -name "*.py" -exec \
        sed -i 's/^import mock$/from unittest import mock/;s/from mock /from unittest.mock /;s/from mock.mock /from unittest.mock /' {} +
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python -m pytest
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
