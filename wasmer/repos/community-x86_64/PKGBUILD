# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=3.0.0
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rust' 'cmake' 'llvm')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/v${pkgver}.tar.gz")
sha512sums=('15d8cace3c878a49ff504538eacc548fff4da68a154bffd34eedcef4445ccf5e0fee8f1470e2f8a652deb8957acdd5697f758038d83ea862d936fd361b7cc43f')
options=('staticlibs' '!lto')

build() {
  cd "$pkgname-$pkgver"
  make WASMER_INSTALL_PREFIX=/usr ENABLE_LLVM=1
}

check() {
  cd "$pkgname-$pkgver"
  make test-compilers test-examples
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
