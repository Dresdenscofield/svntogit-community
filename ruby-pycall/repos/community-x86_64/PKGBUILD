# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=pycall
pkgname="ruby-${_gemname}"
pkgver=1.4.1
pkgrel=1
pkgdesc='Calling Python functions from the Ruby language'
arch=('x86_64')
url="https://github.com/mrkn/pycall.rb"
license=('MIT')
depends=('ruby' 'python')
checkdepends=('ruby-rake' 'ruby-rake-compiler' 'ruby-rspec')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2704e0d28533ddab3c972e69c5c08eea04e6184fd1fc1670302d7471013468de07b1ae0200bbaf030cf5b266f2ff07b2c050682f5e050d7b86b819673bdbe695')

prepare() {
  cd pycall.rb-$pkgver

  # we build based on a tar archive, not a git repo
  sed --in-place --regexp-extended 's|git ls-files -z|find . -type f -not -path "*/\.git*" -printf "%P\\\\0"|' "${_gemname}.gemspec"
}

build() {
  cd pycall.rb-$pkgver

  gem build "${_gemname}.gemspec"
}

check() {
  cd pycall.rb-$pkgver

  rake test
}

package() {
  local _gemdir="$(gem env gemdir)"
  cd pycall.rb-$pkgver
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/${_gemname}-${pkgver}/ext
}
