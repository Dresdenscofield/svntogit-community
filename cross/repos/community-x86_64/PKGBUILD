# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cross
pkgver=0.2.3
pkgrel=1
pkgdesc="'Zero setup' cross compilation and 'cross testing' of Rust crates"
url="https://github.com/rust-embedded/cross"
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
optdepends=('docker: provide container'
            'podman: provide container')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-embedded/cross/archive/v${pkgver}.tar.gz")
sha256sums=('44bbe0bbd4fda524fa2d3a9dd9d9561a27b2f15b9c1c2edc2e59247379bce9ad')
b2sums=('84918483aa44d8592e8ca91bc142ebc84cfe33aa5f0882772a0d847cb14b3dcd8f7c7bf702c31aa4990b6ff3b2e9351480678c473fe15ec4ff31d4d85ab2c627')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  depends+=('rustup')
  cd ${pkgname}-${pkgver}
  install -Dm755 "target/release/cross" -t "${pkgdir}/usr/bin"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    LICENSE-APACHE LICENSE-MIT
}

# vim: ts=2 sw=2 et:
