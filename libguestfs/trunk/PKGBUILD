# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Evaggelos Balaskas <eblaskas _AT_ ebalaskas _DOT_ gr>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Nikos Skalkotos <skalkoto (at) Gmail.com>

pkgname=libguestfs
pkgver=1.48.5
pkgrel=1
pkgdesc="Access and modify virtual machine disk images"
arch=(x86_64)
url="https://libguestfs.org/"
license=(GPL2 LGPL2.1)
# NOTE: see appliance/packagelist.in
_appliancedeps=(
  btrfs-progs
  debootstrap
  dhcpcd
  dosfstools
  exfatprogs
  f2fs-tools
  gptfdisk
  grub
  iproute2
  iputils
  jfsutils
  libldm
  lrzip
  lsof
  lsscsi
  lvm2
  lzop
  mdadm
  mtools
  multipath-tools
  nilfs-utils
  ntfs-3g
  openssh
  pciutils
  procps-ng
  psmisc
  reiserfsprogs
  rsync
  squashfs-tools
  strace
  syslinux
  systemd-sysvcompat
  vim
  xfsprogs
  yara
)
depends=(
  augeas
  fuse2
  hivex
  jansson
  sleuthkit
  libconfig
  libvirt
  perl-libintl-perl
  qemu
  supermin
  "${_appliancedeps[@]}"
)
makedepends=(
  bash-completion
  cdrtools
  cpio
  erlang-nox
  ghc
  go
  gobject-introspection
  gperf
  ocaml
  ocaml-compiler-libs
  ocaml-findlib
  perl-module-build
  python
  java-environment
  lua
  php
  ruby
  ruby-rake
  ruby-rdoc
  rust
  vala
)
checkdepends=(linux ruby-test-unit)
optdepends=(
  'gobject-introspection: GObject-Introspection Bindings'
  'java-runtime: for Java bindings'
  'ocaml: OCaml Bindings'
  'php: for PHP module'
  'python: Python Bindings'
  'ruby: for Ruby bindings'
)
provides=(libguestfs.so libguestfs-gobject-1.0.so)
backup=(etc/libguestfs-tools.conf)
options=(debug)
source=(
  http://download.libguestfs.org/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
  $pkgname-1.48.1-disable_php_tests.patch
  $pkgname-1.48.5-fix_xfs_growfs_test.patch::https://github.com/libguestfs/libguestfs/commit/e657e45b439723c9cbc7c2c51dad4cdb88cf7cc7.patch
)
sha256sums=('19bc3fbfd0a590a6f8775d004b11db4c699cc423562f157ead03cf6bd73eda8d'
            'SKIP'
            '151d7ab43e7150c7d645eb0bad9681ce5ecc59cae44f254f08ef617ce3d9932f'
            '33fa469b5f2c9b228906d27a3a36d2fbcd3bc288575e89cab7345c96d62437ba')
b2sums=('c33bf18bd80cc19e27e7006cf60c4e962bb058bd8d70fea150fc2b72f0721118a9ca738ead42f38f7982a1aeb0ee6a15d82a752f688c7e65a3c9d92c3e509b7b'
        'SKIP'
        '2f7c429875d80b2b3a70286f83764ce6ed8be86055232fc059ab55ebf8393f9d29a2241401c7ab55af7ba9180ea85909647fe06ca51880f93bc66b488bcec78c'
        '2107b4125b057234dd6a612920496118e6ab35cf99ac7c11f0498062fdc637d2792b95cbecbdcffa06becef62f8fe84465ecac01278e4214aab155149a328e15')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  # disable php tests, as missing arginfo definition makes them fail: https://github.com/libguestfs/libguestfs/issues/78
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.48.1-disable_php_tests.patch

  # fix issue with test_xfs_growfs_0: https://github.com/libguestfs/libguestfs/issues/100
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.48.5-fix_xfs_growfs_test.patch

  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  # TODO: build haskell bindings
  CFLAGS+=" -ffat-lto-objects" \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static \
    --disable-haskell \
    --with-extra-packages="libldm systemd-sysvcompat openssh qemu" \
    --with-guestfs-path=/usr/lib/guestfs

  make
}

check() {
  # skip failing test_file_architecture_26: https://github.com/libguestfs/libguestfs/issues/100
  export SKIP_TEST_FILE_ARCHITECTURE_26=1
  LIBGUESTFS_TRACE=1 make -k check -C $pkgname-$pkgver
}

package() {
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install -C $pkgname-$pkgver
}
