# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-tauri
_pkgname=tauri
pkgver=1.0.4
pkgrel=1
pkgdesc="Command line interface for building Tauri apps"
arch=('x86_64')
url="https://github.com/tauri-apps/tauri"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d9e39ee9946a1832318a5b28aa8225c646f2e218aadd724edf2ac9eb360e1651f95f16bbd5afb0f353d79d3f631422f94f502523d1fd25fa4088d25585e66c4d')
options=('!lto')

prepare() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo build --release --frozen
}

check() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver/tooling/cli"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE_MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
