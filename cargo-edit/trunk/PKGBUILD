# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-edit
pkgver=0.11.4
pkgrel=1
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo' 'libgit2.so')
options=('debug')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3e4436c2309f40c24aa35aa801dd0057ae1760944025edec9608c2f6f154cea2d0a72b493effbc5fe06d35188bb5acd10c023dbe444547da0208db9d8bf9516d')
b2sums=('d0846127dbd99bbb708bb9483d5f96ee03c61e539d90317da8f27ae8847ad58b5033c2d4fa107959228bb7bf2620ae8a8a2edf91be4b9afa8c98a15b85736eca')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-rm \
    target/release/cargo-upgrade \
    target/release/cargo-set-version \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
