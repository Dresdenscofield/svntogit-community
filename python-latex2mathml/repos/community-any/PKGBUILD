# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-latex2mathml
pkgver=3.63.2
pkgrel=1
pkgdesc="Pure Python library for LaTeX to MathML conversion"
url="https://github.com/roniemartinez/latex2mathml"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-multidict' 'python-xmljson')
source=("https://github.com/roniemartinez/latex2mathml/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('baba4fc38b7b8fe2184991119b6d36e00c3526846f6d06bf1f4c17ed3c5365bad1074eaa93195f667ec69136a0bd59a6334f2aaf649f773239032b6e289ee401')

build() {
  cd latex2mathml-$pkgver
  python setup.py build
}

check() {
  cd latex2mathml-$pkgver
  pytest
}

package() {
  cd latex2mathml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
