# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Adria Arrufat <swiftscythe at gmail dot com>

pkgname=ugrep
pkgver=3.9.0
pkgrel=1
pkgdesc='ultra fast grep with interactive TUI, fuzzy search, boolean queries, hexdumps and more'
arch=(x86_64)
license=(BSD)
url="https://github.com/Genivia/$pkgname"
depends=(bzip2
         lz4
         pcre2
         xz
         zlib
         zstd)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('515bcc96d2ee7ba379ae9c7d2093fb581be43fc1f6d4306bba30c0f4c0e04687')

build() {
	cd "$_archive"
	./configure --prefix /usr
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
