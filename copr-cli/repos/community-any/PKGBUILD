# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=copr-cli
pkgver=1.102
pkgrel=1
pkgdesc="CLI tool to run copr"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-copr' 'python-humanize' 'python-jinja' 'python-setuptools' 'python-simplejson')
checkdepends=('python-pytest' 'python-mock' 'python-responses')
source=("https://pagure.io/copr/copr/archive/copr-cli-$pkgver-1/copr-copr-cli-$pkgver-1.tar.gz")
sha512sums=('ef8778ea132b3e1e6d34ba5add71587438967b74bac89ddb2696533a91b8df44d936ebad1d611369d18478e0ec4766edd825796813d4692e165d1553690eeffc')

build() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py build
}

check() {
  cd copr-copr-cli-$pkgver-1/cli
  python -m pytest
}

package() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 man/*.1 -t "$pkgdir"/usr/share/man/man1/
}
