# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
_binname=tldr
pkgver=1.6.0
pkgrel=1
pkgdesc='A fast tldr client in Rust'
arch=('x86_64')
url='https://github.com/dbrgn/tealdeer'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust')
provides=('tldr')
conflicts=('tldr')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('9450e819fa05bce38db081274acd26d65d80a52007bb8eabeb1ce143cadb018fc3c9ef4a2acf3f2d449a339478b91f91ad36dbdc7c88c9040baf807ff3b0e081')
b2sums=('383fc65f563b74463c5ab93761eb3ca9eb0d60ad12dd1a4312cee3e1d926011a70197a95d3395f7e6f7ee8c35d18ba5afe1143474d6b1d6fe4803f8803b0f3c7')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"

  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$_binname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  cd completion
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/$_binname"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/$_binname.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_$_binname"
}

# vim:set ts=2 sw=2 et:
