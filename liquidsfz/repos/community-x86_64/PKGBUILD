# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=liquidsfz
pkgver=0.3.0
pkgrel=1
pkgdesc="SFZ Sampler"
arch=(x86_64)
url="https://github.com/swesterfeld/liquidsfz"
license=(LGPL2.1)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc)
makedepends=(jack libsndfile lv2)
checkdepends=(lv2lint)
optdepends=(
  'jack: for standalone application'
  'lv2-host: for LV2 plugin'
)
provides=(libliquidsfz.so soundfont-synthesizer)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/swesterfeld/${pkgname}/archive/${pkgver}.tar.gz
  $pkgname-0.3.0-vector_include.patch::https://github.com/swesterfeld/liquidsfz/commit/82ce2bb1c18c4addfbda94643dcf7f7b836a13d7.patch
  $pkgname-0.3.0-vector_fix.patch::https://github.com/swesterfeld/liquidsfz/commit/01f0eb84f8efb3728f8627c9c37176902bba8e0a.patch
  $pkgname-0.3.0-functional_include.patch::https://github.com/swesterfeld/liquidsfz/commit/a70b21a5310891b15c1daef2a039297f71794347.patch
)
sha512sums=('01635c258fe677ed2bc60f262109a3864e7496c1310b5e74b21cf17bc5d638657201c251dd857301078d5530e0bf0c3e78c8d7202b3c108764760680c388ffc3'
            '31aee90752d5ff4f3b9558df99de1cd457581bc64ed393fab57d068be91a27e9d95f78d14349908902a97d96b6beed05d278e8d64a7ba9cea96592fdd710ee69'
            '2de84a4ac86bb4901c2bbd18f5bd9c644605a97b1befcbbd70abf2142c152fde1f5b730e70e0a6fb04b39199f7be3b5da855ebc89716769d800b804fdf3d852b'
            'dc28a3c0649da87dbb302c401229a0a244499dab9b92090374b0565dabe87903d9c92938e3fff3c2ce14d613271e26b205e2b53f36d9a9efa5e248722825c3d8')
b2sums=('ca59c350687dc061530d42f02257cf13521c25e313fabdcbe1a34b11cc7b7cb7218af6006ce48013bacf180a8f34146b664850a985f7292240a9aeb180b6086f'
        '37332c8220dfd13ec22e6f6048b6f2293b46968b3ae2e1a6e8e4e040ab94391caf5e5e0e8c58f45381f0a6f9ca9f0921e898ac1789d065aa9688bac88799d752'
        'bd7323dea88517bfa4362fdb48606bd12397b0436dc8bbf568da7bdb347f2cf107957c121d4eb45cd56369679e6337c3508cb7994ceceb75054a8b3fef27f876'
        '0669aa41d4068f0e240df80bf73174ae69a5d66c22e5f1cac635b460e0cc23fa70ad3edbd7be29180832c3bf5b437394c66800979019055208ff01ea8b7c4a9d')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.3.0-vector_include.patch
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.3.0-functional_include.patch
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.3.0-vector_fix.patch
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --disable-static-cxx
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
  LD_LIBRARY_PATH="$pkgname-$pkgver/lib/.libs/:$LD_LIBRARY_PATH" lv2lint -Mpack -I $pkgname-$pkgver/lv2 "http://spectmorph.org/plugins/liquidsfz"
}

package() {
  depends+=(libsndfile.so)

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{NEWS,TODO,{OPCODES,README}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
