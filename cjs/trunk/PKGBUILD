# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=5.6.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('gtk3' 'gobject-introspection-runtime' 'js78')
makedepends=('meson' 'samurai' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('c190990ce5dc6b0840efc4207c031b2d174d8d65a325d854903b2fa35b52a949307d245920200d9615441486ebf5259d4b1a14ae3a98ca930939173d72273905')
b2sums=('57ac316dd327a100e3fbb81e62b77c426869aec12eb2fe7bf5a000ef8b5f725bab04fe68c42299797ab980e93c13b6902a01f0f1a46eee6a496f6d548de8ed0f')

build() {
    mkdir -p ${pkgname}-${pkgver}/builddir
    cd ${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir=/usr/lib \
          --buildtype=plain \
          -Dinstalled_tests=false \
          ..

    samu
}

check() {
    cd ${pkgname}-${pkgver}/builddir

    # Needs a display
    xvfb-run meson test --print-errorlogs
}

package() {
    cd ${pkgname}-${pkgver}/builddir

    DESTDIR="${pkgdir}" samu install
}
