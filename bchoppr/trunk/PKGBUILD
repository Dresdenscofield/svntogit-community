# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BChoppr
pkgname=bchoppr
pkgver=1.12.0
pkgrel=1
pkgdesc="An audio stream chopping LV2 plugin"
arch=(x86_64)
url="https://github.com/sjaehn/BChoppr"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(cairo gcc-libs glibc libx11)
makedepends=(lv2)
checkdepends=(lv2lint)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/sjaehn/$_name/archive/$pkgver.tar.gz)
sha512sums=('a2169757e9dbbbff6445dadd8d697f698667ca1cdcd5f3d15a48b95f808bde46ef96b98acb42368e25846e78103d02b1c6c6d0d430ab8e66073239225503e707')
b2sums=('5287558cc68bbcece984976e638a61344d7d59f11209aba16c731ea87422d1edc0dcd3750a7150e050ad393eb15850898fafa81ce140224f3e798ae4222b984b')

build() {
  make PREFIX=/usr -C $_name-$pkgver
}

check() {
  cd $_name-$pkgver
  # ignore symbols that cause issues with higher versions of gcc: https://github.com/sjaehn/BChoppr/issues/15
  lv2lint -Mpack -s "*basic_string*" -s "*codecvt_utf8*" -I "$PWD/$_name.lv2/" "https://www.jahnichen.de/plugins/lv2/$_name"
}

package() {
  depends+=(lv2-host)

  make DESTDIR="$pkgdir" PREFIX=/usr install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # remove license file
  find "$pkgdir/usr/lib/" -type f -iname "*LICENSE*" -delete
}
