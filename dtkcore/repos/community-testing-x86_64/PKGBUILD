# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.6.0.1
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
options=('debug')
depends=('dtkcommon' 'deepin-desktop-base' 'python' 'gsettings-qt' 'lshw')
makedepends=('qt5-tools' 'gtest')
source=("https://github.com/linuxdeepin/dtkcore/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8410f73a5a20dabdbe79633284ee180ca585060f0c6a2553258b9e8cfa064e681d129108d394e9d7bc8278716d7f5525c9aecbf634aefa30adc2b75c435e09b4')

prepare() {
  cd dtkcore-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

check() {
  cd dtkcore-$pkgver
  make check || echo "Tests failed"
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
