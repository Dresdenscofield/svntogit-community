# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Gavin Yancey <gyancey@hmc.edu>
# Contributor: Timofey Titovets <nefelim4ag@gmail.com>

pkgname=bees
pkgver=0.7.1
pkgrel=1
pkgdesc="Best-Effort Extent-Same, a btrfs deduplicator daemon"
arch=('x86_64')
url="https://github.com/Zygo/bees"
license=('GPL3')
depends=('util-linux-libs' 'bash')
makedepends=('btrfs-progs' 'systemd')
options=('debug')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/Zygo/bees/archive/v${pkgver}.tar.gz")
sha256sums=('cd112e4af24966e984bcd72056411859715a0bdcfba57e6db70e63662a3d89bb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make BEES_VERSION="${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}" BEES_VERSION="${pkgver}"

  mv "${pkgdir}/usr/sbin" "${pkgdir}/usr/bin"
}
