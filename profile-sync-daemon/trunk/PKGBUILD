# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.46
pkgrel=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=(any)
url="https://github.com/graysky2/profile-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('zsh-completions: for completion when using zsh')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('cb0dd4c0d4db2f8043f01e04a24d0292b93e4a79482460426ca5f66888385122aaca3cb762a041c685568098b81e6f61e828355cc3e9693921eca11ee9ed6b3e')
b2sums=('fb5c24c9b08634fcc655739d2c7f27bc5599e8c33db46565c658dad06c8fa7d9d18c0f2e3eb9f132c61cbdf22f54b93c418350507a28738e8128de9d0e07b29d')

build() {
  make -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
