# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

# Upstream test suite does not current pass in our build environment. Among
# other issues one test seems to be intended to lint a bogus import, but it
# ends up actually trying to run the code—of course throwing an error. This
# seems to build and work as expected but until I figure out how to either pass
# all tests or disable the ones that don't make sense...
BUILDENV+=(!check)

_pyname=pylint
pkgname=python-$_pyname
pkgver=2.15.5
pkgrel=1
pkgdesc='Analyzes Python code looking for bugs and signs of poor quality'
arch=(any)
url=https://pylint.pycqa.org
license=(GPL)
_pydeps=(astroid
         colorama
         dill
         isort
         mccabe
         platformdirs
         tomli # for python <3.11
         tomlkit)
depends=(python
         "${_pydeps[@]/#/python-}")
makedepends=(python-{build,installer}
             python-myst-parser
             python-setuptools
             python-sphinx{,-reredirects}
             python-wheel)
checkdepends=(mpdecimal
              python-gitpython
              python-pytest{,-timeout})
optdepends=('graphviz: Enable output formats other than dot or vcg'
            'tk: Pylint GUI')
_archive="$_pyname-$pkgver"
source=("https://github.com/PyCQA/pylint/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('05a51f3382651642bae32a4e5036b340f6d3b2e666713fb139ce3bbc274f04dc')

prepare() {
	cd "$_archive"
	# Remove build time dependency pinning that conflicts with Arch packages
	sed -i -E '/^requires/s/~=[0-9.]+//g' pyproject.toml
	# Remove overly restrictive dependency pinning that ends up in runtime checks
	sed -i -e '/>=/{s/>.*//;p}' -e '/python_requires/d' setup.cfg
	# Give ourselves a way to granularly disable test dependent on installed path
	sed -i -e '/^markers =/a\    pathdependent: Does not play nice' setup.cfg
	sed -i -e '/test_do_not_import_files_from_local_directory_with_pythonpath/i\    @pytest.mark.pathdependent' tests/test_self.py
}

build() {
	cd "$_archive"
	python -m build -wn
	make PYTHONPATH="$PWD/build/lib" -C doc man
}

check() {
	cd "$_archive"
	PYTHONPATH="$PWD:$PWD/build/lib" pytest -vv --color=yes \
		-m 'not pathdependent' \
		--deselect tests/benchmark/test_baseline_benchmarks.py
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/_build/man/*.1
}
