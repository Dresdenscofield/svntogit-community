# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pdm
pkgname=python-pdm
# WARNING: vendored libs may change in patch level versions...
pkgver=2.0.2
pkgrel=1
pkgdesc="A modern Python package manager with PEP 582 support"
arch=(any)
url="https://github.com/pdm-project/pdm"
license=(MIT)
depends=(
  python-blinker
  python-cachecontrol
  python-click
  python-dotenv
  python-findpython
  python-installer
  python-packaging
  python-pdm-pep517
  python-pep517
  python-pip
  python-platformdirs
  python-requests-toolbelt
  python-resolvelib
  python-rich
  python-shellingham
  python-tomli
  python-tomlkit
  python-unearth
  python-virtualenv
  python-wheel
)
makedepends=(python-build)
checkdepends=(python-pytest python-pytest-mock python-pytest-xdist)
optdepends=('python-keyring: for storing credentials')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('054457765343de220cd12c404378cbfaacabc0f2ef93d4c730e4e390fdd92a61968682fcc7de3782f8bc37e3121a15624af200703404b2e1ea6b8124fb85f386')
b2sums=('b02bafa4cfed5f45e5ae8ff5f810fc2c61a950485dae604e59b2d1b48c7cfcc77871afc6b1aaf56a82b762d2bc29228ae02e748d229d5078fbb18280653ca504')

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # skip tests that would fail because of missing interpreters: https://github.com/pdm-project/pdm/issues/1175
  pytest -vv -c /dev/null -k 'not test_basic_integration'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
