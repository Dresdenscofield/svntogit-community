# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=3.8.0
pkgrel=1
pkgdesc="Plugins for gedit"
arch=(x86_64 i686)
license=(GPL)
url="http://live.gnome.org/GeditPlugins"
depends=(gedit python-dbus)
makedepends=(intltool gnome-doc-utils vte3)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal')
options=('!libtool' '!emptydirs')
install=gedit-plugins.install
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz")
sha256sums=('44e8d61ae634898bf3a86be8b6b2fd4f998f85dcf6cb92ec80a508ca245ce6b5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-plugins=all \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
