# $Id
# Maintainer : BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgbase=packagekit-qt
pkgname=(
	"${pkgbase}4"
	"${pkgbase}"
)
pkgver=0.9.2
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPLv2')
url="http://www.packagekit.org/"
makedepends=(
"packagekit" 'qt4' 'cmake' 'qt5-base'
)
source=(
	"http://www.freedesktop.org/software/PackageKit/releases/PackageKit-Qt-$pkgver.tar.xz"
	'0009-Disable-namespacing-and-classes-names-on-the-interfa.patch'
)
sha256sums=(
	'd5a20d2a477c7b77feaabb7d1eb10d00e2168f81ae4b08a34398bbeded12f4ce'
	'47c9d5e8184a865fb4ffdc3bee5618b3b5997931f81607f93e762f192791a64d'
)

prepare() {
	cd "$srcdir/PackageKit-Qt-$pkgver"

	patch -p1 -i "$srcdir/0009-Disable-namespacing-and-classes-names-on-the-interfa.patch"

	mkdir "qt4"
	mkdir "qt5"
}

build() {
	cd "$srcdir/PackageKit-Qt-$pkgver/qt4"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib ..
	make

	cd "$srcdir/PackageKit-Qt-$pkgver/qt5"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib -DUSE_QT5=1 ..
	make
}

package_packagekit-qt4() {
	pkgdesc="QT4 bindings for PackageKit"
	depends=('packagekit' 'qt4')

	cd "$srcdir/PackageKit-Qt-$pkgver/qt4"
	make DESTDIR="$pkgdir" install
}

package_packagekit-qt() {
	pkgdesc="QT5 bindings for PackageKit"
	depends=('packagekit' 'qt5-base')

	cd "$srcdir/PackageKit-Qt-$pkgver/qt5"
	make DESTDIR="$pkgdir" install
}
