# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=antlr4-runtime
pkgver=4.11.0
pkgrel=1
pkgdesc='Antlr C++ runtime'
url='https://www.antlr.org/'
arch=('x86_64')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'python')
license=('BSD')
source=("https://www.antlr.org/download/antlr4-cpp-runtime-${pkgver}-source.zip")
sha256sums=('0599e0029b3718b91eeaa14b6a777f96973be0e1675a1b35cc298e63ade1d194')
# FS#72726
options=(staticlibs)

build() {
  export CFLAGS+=' -ffat-lto-objects'
  export CXXFLAGS+=' -ffat-lto-objects'

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DANTLR4_INSTALL=ON \
    -DINSTALL_GTEST=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
  install -Dm644 "${srcdir}"/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
