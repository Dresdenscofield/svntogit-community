# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: jtheoof <contact@jtheoof.me>
pkgname=swappy
pkgver=1.5.0
pkgrel=1
license=('MIT')
pkgdesc='A Wayland native snapshot editing tool'
arch=('x86_64')
url='https://github.com/jtheoof/swappy'
depends=('cairo' 'gtk3' 'pango')
optdepends=('wl-clipboard: for persistent clipboard'
            'otf-font-awesome: to draw paint icons')
makedepends=('meson' 'scdoc')
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
b2sums=('4e3a6411225bef8f919792eb730ba68442545685f16a067b499936d79ed4210c478799c17a42ff00ab892f29ca58c520a23803ffeebd38ff5822dddc354ace8e'
        'SKIP')
validpgpkeys=('F44D05A50F6C9EB5C81BCF966A6B35DBE9442683')

build() {
    cd "${pkgname}-${pkgver}"
    arch-meson build
    ninja -C build
}

package() {
    cd "${pkgname}-${pkgver}"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
