# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-fourmolu-plugin
pkgname=haskell-hls-fourmolu-plugin
pkgver=1.0.1.1
pkgrel=2
pkgdesc="Integration with the Fourmolu code formatter"
url="https://hackage.haskell.org/package/hls-fourmolu-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-fourmolu' 'haskell-ghc' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-lens' 'haskell-lsp' 'haskell-ghc-api-compat')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-lsp-test')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('44716181a491816325eb3d65404c726a92ca4f1b89344bd77f4358da7d12503b21e30ae7b40b312fe816869b8a83fe846157ad5c41a0f812287961cd1089dc01')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -r ghc:ghc,ghc-api-compat $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
