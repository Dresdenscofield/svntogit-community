# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=genact
pkgver=1.2.1
pkgrel=1
pkgdesc="A nonsense activity generator"
url="https://github.com/svenstaro/genact"
arch=("x86_64")
license=("MIT")
depends=('gcc-libs')
makedepends=("rust")
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('ab62585bb2d96bbf876c1a29f7e70a017f6883f5ba628e9f38b9cb6bc1653462d7f9cacd988b3b83620bf0bceda1050ffc160cd341db27698747a78b402f840b')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --release --locked
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
