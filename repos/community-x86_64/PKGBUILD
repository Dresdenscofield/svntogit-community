# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.9.3
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools' 'cmake')
checkdepends=('python-pytest-runner')
source=("https://github.com/heitzmann/gdstk/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8bf0a776449512befdd8e547631570c526a16ec9d2abaf2ffc58d1714ff5a9dd8fa68e364c6638618b8236202ba1a88e225be9713272a5be0ba2a17c093cce19')

build() {
  cd gdstk-$pkgver
  python setup.py build
}

check() {
  cd gdstk-$pkgver
  python setup.py pytest
}

package() {
  cd gdstk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
