# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=genact
pkgver=1.1.0
pkgrel=1
pkgdesc="A nonsense activity generator"
url="https://github.com/svenstaro/genact"
arch=("x86_64")
license=("MIT")
depends=('gcc-libs')
makedepends=("rust")
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('8f32019efd0cda4e922bb0a57ddb1531fc7e9923b565cd5dc3985c6eceb7e6f709937a25822a59bbf5de1e6ec8a494be6a122c8b0ba7ea97f4de9a23c9d53c7b')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --release --locked
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
