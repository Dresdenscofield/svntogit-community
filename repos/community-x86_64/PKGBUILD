# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brian <brain@derelict.garden>

pkgname=ladybird
pkgver=20220912
pkgrel=1
pkgdesc='Web browser built from scratch using the SerenityOS LibWeb engine'
arch=(x86_64)
url='https://github.com/awesomekling/ladybird'
license=(BSD)
depends=(libgl qt6-base qt6-wayland)
makedepends=(cmake git ninja qt6-tools unzip)
source=("git+$url#commit=954dbd3f5da429d0282435f7d98a86dc7b16417b" # 2022-09-12
        ladybird.sh)
b2sums=('SKIP'
        '8a1124ade2b98ab0b8ef2e03616bdbe4340f16dd493283fccb5f69cb74fc172a936da1f5ea3d346e70792deb56e239170fcae2201d7e8a075d1f82750711b6c3')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -G Ninja \
    -S $pkgname
  ninja -C build
}

package() {
  install -d "$pkgdir/usr/"{lib/$pkgname,share/serenity}
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"

  cp -R build/_deps/lagom-build/*.so* "$pkgdir/usr/lib/"
  cp -R build/serenity/* "$pkgdir/usr/share/serenity/"

  install -Dm755 build/ladybird "$pkgdir/usr/share/serenity/Base/bin/$pkgname"
  install -Dm644 $pkgname/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
