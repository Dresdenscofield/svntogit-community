# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=genact
pkgver=1.0.2
pkgrel=1
pkgdesc="A nonsense activity generator"
url="https://github.com/svenstaro/genact"
arch=("x86_64")
license=("MIT")
depends=('gcc-libs')
makedepends=("rust")
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('76eac5931a53d0f9216429e16cd3562f46c8bffd1ec315274e2862d1ea22f33402c6b95ca3483bb4f78174988aa6cd12a2919976117285480d7833a628a4000e')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --release --locked
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
