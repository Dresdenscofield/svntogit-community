# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=xiphos
pkgver=3.1.5
pkgrel=1
pkgdesc="Bible study tool for GTK+"
arch=('x86_64' 'i686')
url="http://xiphos.org/"
license=('GPL')
depends=('libgsf' 'libglade' 'gtkhtml' 'sword')
makedepends=('gnome-doc-utils' 'intltool' 'python2')
conflicts=('gnomesword')
provides=('gnomesword')
replaces=('gnomesword')
install=xiphos.install
source=("http://downloads.sourceforge.net/gnomesword/$pkgname-$pkgver.tar.gz"
        "$pkgname.patch")
sha256sums=('568474d9060979ad1140dbf0eae13e46890a268bfa06d600ded5917ba6674bc2'
            'd9cdbb2de7108e2d4e49960c0c54470bc34176cb5035e14af53400c6a823ef60')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '0,/on/s//on2/' waf
  patch -p0 -i "$srcdir/$pkgname.patch"
  CXXFLAGS+=" -DNO_SWORD_SET_RENDER_NOTE_NUMBERS=1" ./waf configure --prefix=/usr --enable-gtkhtml
  ./waf build --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 ./waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
