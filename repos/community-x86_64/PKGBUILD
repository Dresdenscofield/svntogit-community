# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
pkgver=0.3.9
pkgrel=1
pkgdesc="GObject-based library to access oVirt REST API"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/libgovirt"
license=(LGPL)
depends=(librest)
makedepends=(git gobject-introspection meson)
_commit=339bd3322f02079494da3e1fee61fba31dd42196
source=("git+https://gitlab.gnome.org/GNOME/libgovirt.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/^v//' -e 's/-/+/g'
}

build() {
  meson build $pkgname \
    --prefix=/usr
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
