# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.9.2
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools' 'cmake')
checkdepends=('python-pytest-runner')
source=("https://github.com/heitzmann/gdstk/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f97e5519341fe040cd7ddea709fa571d59c6a463579eb725949e62c44f913190a3bd9bf1e44f01d5e699cc428b43caad56841e2b0ae9fe3ebaf77ec4b7985056')

build() {
  cd gdstk-$pkgver
  python setup.py build
}

check() {
  cd gdstk-$pkgver
  python setup.py pytest
}

package() {
  cd gdstk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
