# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=3.8.3
pkgrel=1
pkgdesc="Plugins for gedit"
arch=(x86_64 i686)
license=(GPL)
url="http://live.gnome.org/GeditPlugins"
depends=(gedit python-dbus)
makedepends=(intltool gnome-doc-utils vte3 libgit2-glib)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal'
            'libgit2-glib: for git plugin')
options=('!libtool' '!emptydirs')
install=gedit-plugins.install
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz")
sha256sums=('2340910ed49acfaab7e8d30142e087f70e55c1a55f8b755d4f9014bce3923c80')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-plugins=all \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
