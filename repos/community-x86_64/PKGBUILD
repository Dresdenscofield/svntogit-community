# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.83
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('011f37abaf19afd004ee2942731a7781d5cad7307aefca6b738874fcf468ff6e9c0bb7df229ff411f5439f1eec6da7ccf295ebcd87517aa27a7848efa620237e'
            '0d2097ba297b1609a933ff67497a238d6a4f70ed78bda92dceb5b38302709d739990b92f2af9a3ff1416f75e53dabe5cf8ce8467b1d374403d66fd7a5633e9cc')
b2sums=('dbdafc6d737e163890a4cb9d34cc52a19b04ca1ffc951e86e5600335d2110cf2c49d9cd6dd42e1dc3131d359ba638812d8c4d79552c836a3a3c1581745597dfc'
        '8b0791ee0a01406db0b2b89a728a404c0b37149c5a87ca13b9fbb3c3ace649f0a9030eab7e1cf9c58b3716ca35bd2f9d95440f4f827f765064892c2bce9f7107')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
