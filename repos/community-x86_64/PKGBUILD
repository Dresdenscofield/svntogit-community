# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.18
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkwidget' 'freeimage' 'libmediainfo' 'opencv')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/image-editor/archive/$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('b19e6cf8b886343b94da52a3389f547ad11ea5265db62adfeabdd9112a9a1dc98d3490722c83bf9dd725eaef534ce3efbaa039f6ee1e0df1b1153f80b81e7661'
            'd61cf129107d820e59c4243017551409cdedbbca1ec2c0860c28ffac635c412b6ed83b8f3a399ad5679fda6658a66f0c2999b8b43dfdad1f6e85c3d3a32eedd7')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
