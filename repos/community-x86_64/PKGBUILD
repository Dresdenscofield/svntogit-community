# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=goaurrpc
pkgver=1.1.1
pkgrel=2
license=('MIT')
pkgdesc="An implementation of the aurweb /rpc service in go"
depends=('glibc')
makedepends=('go')
arch=('x86_64')
url="https://github.com/moson-mo/goaurrpc"
source=("${pkgname}-${pkgver}::https://github.com/moson-mo/goaurrpc/archive/refs/tags/v${pkgver}.tar.gz"
        goaurrpc-local.sample
        goaurrpc-remote.sample
        goaurrpc.service
        goaurrpc.sysusers)
sha256sums=('98a0fbe2f64d552a265adcb80c94809c93e75f65f9b12e03d3e6df5427c237f0'
            '17007dec9e239832a1875846df4c6f2b3eb012da1649c363c87c2e21daf4ef11'
            '033ceab29e772ba7b54eed911f93f04c6dd88252e70ed046c5f1f0e6b7d74ffb'
            'e6a298e647fcf3b255d9886180b992f0dc447e763509c50513473f6d9b9ac5e3'
            '45ac878e62e8e43c55645ebbd71fc8e10ce32c81d1a769979f48ac5f25392ff3')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p dist/
}

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o dist/goaurrpc \
    .
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 dist/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 "${srcdir}"/${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -vDm 644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "${srcdir}"/goaurrpc-local.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 "${srcdir}"/goaurrpc-remote.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
