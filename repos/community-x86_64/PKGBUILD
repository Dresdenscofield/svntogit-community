# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=2.0.1
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('9f8c3932c2110a80c9f0515ccb6132d4670aabab1281bdc068310721350073403af325f5f13c655a460de0d9830b37b9357a36dfc0aeea91613d668e52c496f0'
            '1a06a7cbd6616753e09831613cc908d687362e68397dc409c881deacfa72aecfb4a9d1fb775282984e89e2cc892a7d6fd6cfcbb9af81354f8e40c0e488ae3fd8')
b2sums=('c598b6f6e3be25e3e63b9c9ee347b0c040a058c5df97998f9390d8cd3230cdc987ee0a4eb5ee75e399f5365b82a22fc33d7f50505d75d25dbe72edba10d2e6ef'
        '735312e44e76b441568453185deac30ce9f698c9f11a7dbcb776d04da0dcad3ccc9f98d7eefc5d505dd49ab6fbcec6e55693c729b8ccbe3b8480ddcd03d40760')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
