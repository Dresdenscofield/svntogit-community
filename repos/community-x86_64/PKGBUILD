# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: peeweep <peeweep at 0x0 dot ee>

pkgname=neovide
pkgver=0.10.2
pkgrel=1
pkgdesc='No Nonsense Neovim Client in Rust'
arch=(x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(fontconfig
         freetype2
         libglvnd
         neovim
         sndio)
makedepends=(cargo
             cmake
             gtk3
             make
             python
             sdl2)
optdepends=('vulkan-intel: vulkan support for intel')
_archive=("$pkgname-$pkgver")
source=("$url/archive/$pkgver/$_archive.tar.gz"
	"Cargo.lock")
sha256sums=('1c2a26419fe7f09239bd40591b8458af012bcb018ce83179c9f76970f12ae66b'
            '6f426669e4997a37d4d523cdfdfca85d17730d3b19baa088f095e1d2712c0169')

updlockfiles() {
	cd "$_archive"
	cargo update
	cp Cargo.lock "$outdir/"
}

prepare() {
	cd "$_archive"
	# https://github.com/neovide/neovide/issues/1628
	cp "$srcdir/Cargo.lock" .
	sed -i -e '/^incremental/a opt-level = 3' Cargo.toml
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	cargo build --frozen --release --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/applications/" "assets/$pkgname.desktop"
	for px in 16 32 48 256; do
		install -Dm0644 "assets/$pkgname-${px}x${px}.png" \
			"$pkgdir/usr/share/icons/hicolor/${px}x${px}/apps/$pkgname.png"
	done
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
