# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.4.20
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('cmake' 'ninja' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('432e5cddadd319d61876f7f46b574bd3a2abd4c2e478d5c692e226645d0315c39cfa0ec50f439d4cb61c8be9b48aa5fd10668a18f14c77956f5b0118bb75ea07')

prepare() {
  mkdir -p build
  sed -i 's|DESTINATION /lib/systemd/user|DESTINATION /usr/lib/systemd/user|' dde-clipboard-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
