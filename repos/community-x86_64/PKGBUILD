# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.6.2
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base qt5-declarative qt5-networkauth qt5-svg qt5-websockets)
makedepends=(help2man meson jack python-jinja python-yaml)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('27be0bd12e2f88e281fa69dc5043fa2029772c19a6ac5426addc95b7b670ac2794d3e6c2d97f479b5bfa3d4caa3cc08e48541fa581901a5afa9c3f6a42e13a76')
b2sums=('a6b5585a19af9cbec6b4866b2ed5519c407d0ab2f2b7e4b8a50b16c157681ab0995cc16d4283b94ecd83b509e79f27e049b7582499f50525e730b02d92be9b3b')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
