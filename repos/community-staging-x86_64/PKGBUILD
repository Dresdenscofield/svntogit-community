# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-sugar
pkgname=haskell-tasty-sugar
pkgver=1.3.0.1
pkgrel=2
pkgdesc="Tests defined by Search Using Golden Answer References"
url="https://github.com/kquick/tasty-sugar"
license=("custom:ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-filemanip' 'haskell-kvitable' 'haskell-logict' 'haskell-microlens'
         'haskell-optparse-applicative' 'haskell-prettyprinter' 'haskell-tasty')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-pretty-show' 'haskell-raw-strings-qq'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit')
_commit=98f2d2bff5dc2e1a93671e4d271bf1db9c81db1f
#source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
source=("$pkgname-$_commit.tar.gz::https://github.com/kquick/tasty-sugar/archive/$_commit.tar.gz")
sha256sums=('583198bee54f0850596280e10e2315a034304ea4a1dabcdcb8c69166ee35a703')

build() {
  cd $_hkgname-$_commit

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$_commit
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$_commit

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
