# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-bdd
pkgver=6.0.0
pkgrel=1
pkgdesc='BDD library for the pytest runner'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-bdd'
depends=('python-glob2' 'python-mako' 'python-parse' 'python-parse-type' 'python-py' 'python-pytest')
makedepends=('python-setuptools')
source=("https://github.com/pytest-dev/pytest-bdd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5223190a1b1d80761059cc34893b64eeca6c4d35b3a81aed186b5d1b4214ac67660cfa6cd39e1953a445c8ca7ceea74cb32b224435193febad35433c2eaf3266')

build() {
  cd pytest-bdd-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-bdd-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-bdd-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
