# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake
pkgver=0.18.2
_commit=a571f0c47cc202a4b46a836b87b7383d84f74fa0
pkgrel=1
pkgdesc='A replacement for rake with a simpler syntax'
arch=(any)
url='https://github.com/ioquatix/bake'
license=(MIT)
depends=(ruby-samovar)
makedepends=(ruby-rspec)
options=(!emptydirs)
source=(https://github.com/ioquatix/bake/archive/$_commit/$pkgname-$_commit.tar.gz)
sha256sums=('7a49936ea048e853351461e62fd9553e2b8a61c85b7ad53b4031eafc868306bd')

prepare() {
  cd bake-$_commit
  sed -r -e 's|~>|>=|g' -e '/covered/d' -e '/signing_key/d' -i bake.gemspec spec/spec_helper.rb
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$_commit
  gem build bake.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$_commit
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd bake-$_commit
  cp -a tmp_install/* "$pkgdir"/
}
