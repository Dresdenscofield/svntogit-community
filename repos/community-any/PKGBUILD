# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.47
pkgrel=1
epoch=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=(any)
url="https://github.com/graysky2/profile-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('zsh-completions: for completion when using zsh')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('40ef4d9a234391d33a11c16308263dcb953acbf35df28fe9840fa544467150f269e7d7a567bc53a4e5d98c33611eec816c51226c69cb1dd69c2649c1935066fe')
b2sums=('d00b07997a5dc1ab981aeb96d1288ed1af8f0c7d432758adb64b3fd3b0cb9b9d0bb61106c55153246e3cad18f2f5d73bee7928e4dfb5df4f6c775f041cd968da')

build() {
  make -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
