# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.3.1
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-attrs' 'python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-isodate' 'python-rfc3339-validator' 'python-strict-rfc3339')
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('72564409e80c8dd8578ba2a363865623aa03b8b2324abd37bea31c503632a367bacf81d8de801579b80347906d2b24c14371c9b37b70176e0bf51ca8c6b9e071')

prepare() {
  cd openapi-schema-validator-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd openapi-schema-validator-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
