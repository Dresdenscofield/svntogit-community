# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-cucumber-messages
pkgver=19.1.2
pkgrel=2
pkgdesc="Protocol Buffer messages for Cucumber's inter-process communication"
arch=(any)
url='https://github.com/cucumber/messages-ruby'
license=(MIT)
depends=(ruby)
makedepends=(ruby-cucumber-compatibility-kit ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/cucumber/messages-ruby/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5a7a2c7796e10bb4e5896d13e200b7c803007f0fa8914586740f5e717b4f0b53')

build() {
  local _gemdir="$(gem env gemdir)"
  cd messages-ruby-$pkgver
  gem build cucumber-messages.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-messages-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd messages-ruby-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd messages-ruby-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
