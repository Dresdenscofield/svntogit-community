# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=2.1.0
pkgrel=2
pkgdesc="Molecule Docker Driver"
arch=(any)
url="https://github.com/ansible-community/molecule-docker"
license=(MIT)
depends=(molecule python-docker python-requests python-selinux)
makedepends=(python-build python-installer python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible-core python-filelock python-pytest python-pytest-helpers-namespace)
optdepends=(
  'ansible-core: for Dockerfile validation'
  'docker: for using a local docker setup'
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('3c3dc493eb183ab3acc01738d8fd2260718a44f2c1a15073dac7b9da0899a3672fc5f2bfae98aa27ea2472170ec83d049bf15943fdbccf6dc1e4efd39d833450')
b2sums=('c4c86520f779b52d15f7c5c0985f3b54edc9d19a39afd1afbe2610fbb733b12731c1f3dc16026c02680286904633dce74d5708495dffc03ac54fd59554300415')

build() {
  cd $pkgname-$pkgver

  # remove pip from build backends because it is in fact not required and just
  # YOLO installs things
  sed -e '/pip/d' -i pyproject.toml

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -c /dev/null -vv --ignore "src/molecule_docker/test/test_func.py"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"

  rm -rv "$pkgdir/$site_packages/molecule_docker/test/"
}
