# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Francesco Minnocci <ascoli.minnocci@gmail.com>

pkgname=python-bleak
pkgver=0.15.1
pkgrel=1
pkgdesc='Bluetooth Low Energy platform Agnostic Klient for Python'
arch=(any)
url=https://github.com/hbldh/bleak
license=(MIT)
depends=(
  python
  python-async-timeout
  python-dbus-next
  python-service-identity
  python-typing_extensions
)
makedepends=(
  bluez-utils
  git
  python-setuptools
)
_tag=ac17549780b1540420354db07bfc640b35492bcd
source=(git+https://github.com/hbldh/bleak.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd bleak
  git describe --tags | sed 's/^v//'
}

build() {
  cd bleak
  python setup.py build
}

package() {
  cd bleak
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-bleak/
}

# vim: ts=2 sw=2 et:
