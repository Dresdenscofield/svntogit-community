# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=repod
pkgver=0.2.1
pkgrel=1
pkgdesc="Tooling to maintain binary package repositories"
arch=(any)
url="https://repod.archlinux.page/"
license=(GPL3)
depends=(
  pyalpm
  python-aiofiles
  python-email-validator
  python-jinja
  python-magic
  python-orjson
  python-pydantic
  python-pyxdg
  python-pyzstd
  python-subprocess-tee
  python-tomli
)
makedepends=(
  just
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-sphinx
  python-sphinx-argparse
  python-sphinxcontrib-programoutput
  python-wheel
)
checkdepends=(python-pytest python-pytest-asyncio python-pytest-lazy-fixture)
source=(
  https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
)
sha256sums=('3e97e8d422030c59561cbd2620b430e1be2bfdb235a9b97fc887053ec97515bf')
b2sums=('315c83251ece30f88803fffed020cf1c98c173b3fc5fe29ea8e9b6654f97a668fad9f43c75b669b276a4bc1561cc46c33678339751df4267606c91cce3b7f51b')

build() {
  cd $pkgname-$pkgver
  just build
}

check() {
  cd $pkgname-$pkgver
  just check
}

package() {
  cd $pkgname-$pkgver
  just --set destdir "$pkgdir" install
}
