# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.3.4
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('118f8bc5719265d150c644cdf3c39e9acdb70de258484346b223ee5defdd78462d17646d34eff784ce7d12936f4f299c3be1eeec5990ca68491b5c5a9d162d7c')

build() {
  cd mockito-python-$pkgver
  python setup.py build
}

check() {
  cd mockito-python-$pkgver
  pytest --ignore=build
}

package() {
  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
