# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=exploitdb
_pkgver=2022-11-12
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="Offensive Securityâ€™s Exploit Database Archive"
arch=('any')
url="https://www.exploit-db.com/"
license=('GPL')
optdepends=('libxml2: to check nmap XML results'
            'xclip: copy paths to the clipboard')
backup=('etc/searchsploit_rc')
options=('!strip')
source=("https://gitlab.com/exploit-database/exploitdb/-/archive/$_pkgver/exploitdb-$_pkgver.tar.gz"
        "rcfile.diff")
sha512sums=('22f26e993377adfd5ee3fe43d02c2249da1aa72f02e43db38420aa4077cbbd19c58d08024c95c64a4220c99979f15ab03e63eddcb6d7d153001199d30a1d4886'
            'e170da5b974975c6c23c5b09e8c1825bbb17446fb506f2d00f92f86f94394acf18a53458bd90969de38da642fc1f7abb08ba940969108002dab17415b88211d6')
b2sums=('9b383cf7e58d9ac7603f0d2fc90de831380d5df9324df74a4a39fda9157114b6c568df5ba892458b8cfd297fd77c3895bade6d1f4271b60672085f0a7aedee2a'
        'f033a50db6358297580363d62bbd9d4a6003b5476ba361650133841138af76b990ed49e2e34d487cf4167ebeb3cd6b8f9230e5e700a5d96f7f06cbc5b42c4076')

prepare() {
    cd ${pkgname}-${_pkgver}

    patch -p1 -i ../rcfile.diff
}

package() {
    cd ${pkgname}-${_pkgver}

    install -Dm755 searchsploit "${pkgdir}"/usr/bin/searchsploit
    install -Dm644 .searchsploit_rc "${pkgdir}"/etc/searchsploit_rc
    mkdir -p "${pkgdir}"/usr/share/${pkgname}
    cp -a exploits/ shellcodes/ files_*.csv "${pkgdir}"/usr/share/${pkgname}
}

# vim: ts=4 sw=4 et:
