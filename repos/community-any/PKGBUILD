# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=vint
pkgver=0.3.20
pkgrel=5
pkgdesc='Lint tool for Vim script Language'
arch=('any')
url=https://github.com/Vimjas/vint
license=('MIT')
depends=('python-ansicolor' 'python-chardet' 'python-setuptools' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
conflicts=('python-vint')
replaces=('python-vint')
source=("$url/archive/v$pkgver/vint-v$pkgver.tar.gz")
sha512sums=('a71437cf20361d86fccfdccc05bebd30b40941ebbf65d587eb158d5e4ed3fdcb807b2291de082df7b9f66f4ccf1832ee0797156133a6c152536bd1915ffc4da4')

build() {
  cd vint-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd vint-$pkgver
  pytest
}

package() {
  cd vint-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/vint
  ln -s "$site_packages"/vint-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/vint/LICENSE.txt
}
