# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=6.9.0
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('GPL3')
depends=(python ansible-core yamllint 
  python-{ansible-compat,black,enrich,filelock,jsonschema,pyaml,packaging,rich,ruamel-yaml,wcmatch})
makedepends=(python-{build,installer,setuptools,wheel})
optdepends=('ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_use_scm_version.patch
        disable_version_check.patch)
b2sums=('e57f814406e3532b269d1c1e2b7c5e89a606ddc482f717cfb2ce5849c7010067b028d7e509dbabea395c0e8786e75666798fc1bf24bb64d9cb31c5b0ae553fef'
        'cf911567b040c8938ca589fde3c93b39cfba29ca1a373b8af354450a4f880d475463b0c81440fb9bfacc7b1f93736a5ae5ab58c7fdb30d8f03d1996754a738f3'
        '09a8a69cd2953e6f0b813976fae31388db87647e80ef7942a960adf1cf71e6a843ed86a2456e718042c790a8481db3bb2f7092c52c9553950cc05f46fe7ee35f')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  patch -Np1 < "${srcdir}"/disable_use_scm_version.patch
  # stop ansible-lint from calling home every 24h to check for a new version
  patch -Np1 < "${srcdir}"/disable_version_check.patch
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
