# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-tinycss2
pkgver=1.2.0
pkgrel=1
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD')
arch=('any')
depends=('python-webencodings')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
_css_tests_commit=c5749e51dda3868b3e8062e65a36584c2fec8059
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz"
        css-parsing-tests-$_css_tests_commit.tar.gz::https://github.com/SimonSapin/css-parsing-tests/archive/$_css_tests_commit.tar.gz)
sha512sums=('d4e6bc3d7e5e1fd6e65bd2b7c6c82012f5e453ea896260f29f41990b6b365e37610837c93ac0869ff8e8240dffbdc25e4286180dee0e4d6e930a9d8f7b700fc6'
            'ef44eb1144a6484f308fabe4187310b2ef0067f328bf5213969d87c475fccae3d716dc66640a0a554ab2a3d7da0efb3a168a288ac6eef7722f3ae46959943b49')

prepare() {
  cd tinycss2-$pkgver
  cp ../css-parsing-tests-$_css_tests_commit/* tests/css-parsing-tests/
}

build() {
  cd tinycss2-$pkgver
  python -m build -nw
}

check() {
  cd tinycss2-$pkgver
  PYTHONPATH=. pytest
}

package() {
  cd tinycss2-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
