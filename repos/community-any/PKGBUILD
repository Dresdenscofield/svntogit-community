# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.14.0
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('a6729fd7b2042d709079ab8f1bcd0c6efc5378186a245d8d238161f0244a559cb09241f71aeabd6f92ee522c4b1a093e1528bf7bfd610404b18b05d64ba3c99b')
b2sums=('db7a17dd3dad057ecfa123c27ab5bf4629ecd8b16106c8bb9c8cc004b2a5e822bd4778ad88b782fa4a75abb12234fe4c54cac63b656893c70ec3c6e37a90b6c6')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
