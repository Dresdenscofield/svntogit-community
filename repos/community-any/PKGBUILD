# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.12.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('0bcaf98cb7d6b22b8abf5a449b37b409b81f64c990052abb2afcda8ba9dbfa1bb277b8d19f5078c0d99c1ef10a36af03e4d33264b99af4e5297595483addfebb')
b2sums=('fb72918baea9a39e5cc8702a4f5c8219b9165a23f2b73f5b1006c876c4708253612542da56276bd5009f20ffce83e450b7e1bc78dd87c0dc6e0bb111bcafc1e0')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/788
    --deselect tests/test_workspacebuilder.py::test_automatic_rename_option
    # https://github.com/tmux-python/tmuxp/issues/792
    --deselect tests/test_workspacebuilder.py::test_environment_variables
  )
  cd $pkgname-$pkgver
  pytest -vv "${_deselected[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
