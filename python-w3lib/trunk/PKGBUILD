# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=2.0.0
pkgrel=1
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/w3lib"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('392ec7563e3e72bb8795c67e1378d6f8eb727e82e1a9bf57f03158a43a4d8a7cf3658ddd1e4b04c25e3cdf8ca22154a1bf273a4535e13bfd3ac4a24f7fac7642')

build() {
  cd w3lib-$pkgver
  python setup.py build
}

check() {
  cd w3lib-$pkgver
  pytest
}

package() {
  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
