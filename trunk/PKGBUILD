# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=4.2.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="https://docs.python-zeep.org"
depends=('python-attrs' 'python-isodate' 'python-lxml' 'python-platformdirs' 'python-requests'
         'python-requests-toolbelt' 'python-requests-file' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-pytest-cov'
              'python-requests-mock' 'python-pytest-httpx' 'python-pytest-asyncio' 'python-aiohttp'
              'python-aioresponses' 'python-xmlsec')
source=("https://github.com/mvantellingen/python-zeep/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('540ff6087a79031a219839e4ba83041f9fea64558df1d2afb580a8c363c2193aec1b2b3b86ddc2283e3f533d00dbc01679be8d9c88d0864ffcf2d49c272f3b94')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
