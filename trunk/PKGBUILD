# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=toastify
pkgver=0.5.0
pkgrel=1
pkgdesc='Commandline tool that shows desktop notifications using notify-rust'
arch=('x86_64')
url=https://github.com/hoodie/toastify
license=('Apache' 'MIT')
depends=('dbus')
makedepends=('rust')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver-fix-version.patch::https://github.com/hoodie/toastify/commit/fe1e9d70cc7a2f9ba93f8db7732df3d242e8af73.patch")
b2sums=('692a8bdbf1651b20cd583bc6e502d1102e1d6b584095ade27322cec2e04b7260474683099f0037356016615027aab9833e434bcfa283447be28e7ad963b4c69e'
        'b4a99f5c174caea5c4f016ca5995cbfd167632a471b2ee415fbca6f8c9b0a9ea3d7e8fb238c4a8dc4bbf27b606ac992af4ae5682d59d3e551eb86f01b4b435df')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  patch --forward --strip=1 --input=../$pkgname-$pkgver-fix-version.patch
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
