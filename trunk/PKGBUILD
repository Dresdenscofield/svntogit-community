# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.5.4
pkgrel=1
pkgdesc="File identification library for Python"
arch=(any)
url="https://github.com/pre-commit/identify"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-ukkonen python-pytest)
optdepends=('python-editdistance-s: for returning license identifiers')
optdepends=('python-ukkonen: for returning license identifiers')
source=($_name-$pkgver.tar.gz::https://github.com/pre-commit/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('cb2d262def6f1f525cc3ac2cf3d10a16f45566809329025a7e91836865cc9b7f7ac5bf819ecd888867fe222f02f6e168a3f781d59915df0628b164d60df71123')
b2sums=('a90f9883d7b69ce4f38209aed273b10481ee6168f552e36ec959b5f6437423364d85528cec77295f10accc2d707e98e68e4cbe1b47007cc3b2c4deed7afb51d5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
