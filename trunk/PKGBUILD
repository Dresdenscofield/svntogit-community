# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python-flask
pkgver=2.2.1
pkgrel=1
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
optdepends=(
  'python-asgiref: async functions and views'
  'python-dotenv: loading env vars from files'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('e37d840d3b1a188e5a8439ddcd97c8d120a447faebaf0380b7cb39710ae4d1e3e4d2f2eeb30ea85e16ffc8dcc80acfe207e57f193359b5518e4a78fdb12745e7')

build() {
  cd "flask-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "flask-$pkgver"
  PYTHONPATH="build/lib" pytest
}

package() {
  cd "flask-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
