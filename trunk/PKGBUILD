# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.15.0
pkgrel=3
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm13-libs' 'clang13' 'bcc' 'libbpf')
makedepends=('cmake' 'llvm13' 'git' 'linux-headers' 'ninja' 'gtest' 'cereal'
             'asciidoctor' 'xxd')
options=(!strip)
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz"
        binutils-239.patch
        bcc-025.patch)
sha512sums=('916e9afeab301e62472e570ef77a3b9b27b43251880b64f1c5f151583c59a6c61e9ede50f3607044b27c5a6ce1a654f290153bf3f9237ebc0a823b5e6356187a'
            '49d168b4a40898eef688196dfed12423392cd56635c63c2a32e3c5a5fb4a8757fee3f4b31df9ba43736d7c0a3ad38a14d6ceb07cdfc73d8598b48d6c17727ba2'
            '22ef3d05790416b4286ea9931646c3940877da8cf02b9645edbc753a4adb75e90f233b533d0c2c790eb62592c6af6487c02a502ecca8d547ec3d329a2aa71f64')

prepare() {
  cd bpftrace-$pkgver
  patch -p1 < ../bcc-025.patch # https://github.com/iovisor/bpftrace/issues/2340
  patch -p1 < ../binutils-239.patch # https://github.com/iovisor/bpftrace/pull/2287
}

build() {
  cd bpftrace-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DLLVM_DIR=/usr/lib/llvm13/lib/cmake/llvm/ -G Ninja -B build
  ninja -C build
}

package() {
  cd bpftrace-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}
