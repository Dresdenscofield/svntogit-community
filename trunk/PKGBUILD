# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-srcinfo
pkgver=0.0.8
pkgrel=7

pkgdesc="Simple .SRCINFO parsing library"
url='https://github.com/kyrias/python-srcinfo'
arch=('any')
license=('ISC')

depends=('python' 'python-parse')
checkdepends=('python-setuptools' 'python-nose')

source=("https://pypi.python.org/packages/source/s/srcinfo/srcinfo-$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/s/srcinfo/srcinfo-$pkgver.tar.gz.asc")

validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5')
sha256sums=('5ac610cf8b15d4b0a0374bd1f7ad301675c2938f0414addf3ef7d7e3fcaf5c65'
            'SKIP')

check() {
	cd srcinfo-"$pkgver"
	python setup.py test
}

package() {
	cd srcinfo-"$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
