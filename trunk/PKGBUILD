# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.7.10
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-installer rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/ijl/$_name/archive/$pkgver.tar.gz)
sha512sums=('1dd3923e1fa9251cee93138a74d0f8eb021a8de8c0e1707a77e1c1090ef031a83b0e29783fac478ad79f1c3f257fad453dfe42c4aa3ac2904c8d7939558f36e3')
b2sums=('fbd266ba20ed961635896968cd4a1d2053cde5fbbf3bdeef566a8c00fec7c502e1502082f9500362a824bd3bcd36f452ae3fa9ea8cbdfec6c2581f9e495186ec')

build() {
  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
