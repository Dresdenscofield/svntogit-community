# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.4.4
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-loguru python-pytest-runner)
optdepends=('python-loguru: for logging support')
source=(https://pypi.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('cded50c57f52e8f572642c1797290b30d27c624abc533ef9a4457965ab4df42fb46cc461a62b2458933ec521cdb4eeb2101b4b44a463a9e68cec7eb119820a9d'
            'SKIP')
b2sums=('9d0900b1e72d924058af0d0dd7016b921aa0056b9393e5ea7feda312ea001d6c7345a3a5cdb02ae7229767c994cbd1a3a16212ac1123656a423d90c47662b140'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
