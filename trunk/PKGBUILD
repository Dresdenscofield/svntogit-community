# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=desync
pkgver=0.9.2
pkgrel=1
pkgdesc="Alternative casync implementation"
arch=(x86_64)
url="https://github.com/folbricht/desync"
license=(BSD)
depends=(glibc)
makedepends=(go)
# NOTE: debug packages for go packages are not yet supported
# options=(debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('ed4e00d03db0f3b2bd246a94f3723296766fe1954e1e9b92c09e3a9e2708cf3569184742748697a5f64c5aa062d55ede7e5016b2558830f7234a8c797d9ba5bc')
b2sums=('47378f7d9d0e808a4ee2d2041849c3ee494cc1d78c3c5567ba02456aac2d81dee0cb8aeaa7d4a901cb6f9e1c145c02da88e085003e383257a77e0cf5f87c469c')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build -o $pkgname ./cmd/$pkgname
}

check() {
  cd $pkgname-$pkgver
  go test ./cmd/$pkgname
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 $pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
