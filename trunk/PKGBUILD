# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=repod
pkgver=0.2.0
pkgrel=1
pkgdesc="Tooling to maintain binary package repositories"
arch=(any)
url="https://repod.archlinux.page/"
license=(GPL3)
depends=(
  pyalpm
  python-aiofiles
  python-email-validator
  python-jinja
  python-magic
  python-orjson
  python-pydantic
  python-pyxdg
  python-pyzstd
  python-subprocess-tee
  python-tomli
)
makedepends=(
  just
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-sphinx
  python-sphinx-argparse
  python-sphinxcontrib-programoutput
  python-wheel
)
checkdepends=(python-pytest python-pytest-asyncio python-pytest-lazy-fixture)
source=(
  https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  https://gitlab.archlinux.org/archlinux/repod/-/raw/97ba3cf2cebda36d15da4aa55a2ef6b213ca0878/justfile
)
sha256sums=('dd8a0d603df5da53378d48c92b6c779d8e03c5b51997c3ea35b3fc0350ca0909'
            '8b9b119213ec07270ebc830abcbdda3748ef74440c53e24625344008a356d4cc')
b2sums=('e62bacfc5a1ec7307f60c43a4e4426b477af4950b0abb46707e8e7fefdcc2ee8cd948af2c8b28fd9b3138b6df5c2cb9a89778ac085b5bccc90c09a94baa95cd6'
        '13fcad256538d81391d12f7fe1574cf570bb09bd5da96bd20a736872d070d17ccc9266294993a4c828220735c40f1c2e983e1e3e7ee3e3a0c4531b30462b8b2a')

prepare() {
  cp -v justfile $pkgname-$pkgver
  sed -e '12d' -i $pkgname-$pkgver/justfile
}

build() {
  cd $pkgname-$pkgver
  just build
}

check() {
  cd $pkgname-$pkgver
  just check
}

package() {
  cd $pkgname-$pkgver
  just --set destdir "$pkgdir" install
}
