# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.13.0
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('06011f9a22ecd83906312feae6a2a3158473add50858521604fe75958f99bad3fee4305a87ef66b5cde597dbe744886f94d1bea70b86bcb2c8e3ae9299b898ae')
b2sums=('29eabf98906f3ace634faec8cf6a8fbaa471d28b7634b46f03a43fe19cca418d9d06610da9d37c21ae94830664ad4be9f818910f6ebc1adb3a3c1bb23605325c')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/788
    --deselect tests/test_workspacebuilder.py::test_automatic_rename_option
  )
  cd $pkgname-$pkgver
  pytest -vv "${_deselected[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
