# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.4.5
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'deepin-image-editor' 'ffmpeg'
         'ffmpegthumbnailer' 'portaudio' 'pulseaudio')
makedepends=('cmake' 'deepin-gettext-tools' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-camera/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ffmpeg-5.patch::https://github.com/linuxdeepin/deepin-camera/pull/211.patch
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/deepin-camera/pull/219.patch)
sha512sums=('10219d5d8b855d499b034f6ec29417f79c3cdcf013186cc7c30d04de83e879709c375d6308ba2ce23b367ab5110cb5230c3a36913312a5abeee81477ddb0d790'
            '525e64b2595db019dc3f7974bfcfb32ffeed62c90b5b51eac31e486154b4e1af115f479d58ed97a17040152d58fc5cd4c3c82c48b4ff764c46183e5a7bdcff1a'
            'c94705cd08c98e82f43ae12417a992185466d36e2d8d8d005e20fe9b7d80bbab0e47fca06ac5715587774fcf870bf778ca489cdbbde7bc8a0535e342164cf3d3')

prepare() {
  cd deepin-camera-$pkgver
  patch -p1 -i ../$pkgname-ffmpeg-5.patch
  patch -p1 -i ../$pkgname-ldflags.patch
} 

build() {
  cd deepin-camera-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  ninja
}

package() {
  cd deepin-camera-$pkgver
  DESTDIR="$pkgdir" ninja install
}
