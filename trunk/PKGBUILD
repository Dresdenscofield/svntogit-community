# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Médéric Boquien <mboquien@free.fr>

pkgbase=python-astropy
pkgname=(python-astropy python2-astropy)
pkgver=1.0.6
pkgrel=1
pkgdesc="A community python library for astronomy"
arch=(i686 x86_64)
url="http://www.astropy.org/"
license=(BSD)
makedepends=(cython cython2 python-scipy python2-scipy python2-h5py python-h5py cfitsio erfa)
source=("http://pypi.python.org/packages/source/a/astropy/astropy-$pkgver.tar.gz")
md5sums=('9012885c35a05573d2fa61f5df220c8d')

prepare() {
  cp -r astropy-$pkgver python2-astropy-$pkgver
}

build() {
  cd astropy-$pkgver
  python setup.py build --use-system-cfitsio --use-system-erfa --use-system-expat # --use-system-wcslib

  cd ../python2-astropy-${pkgver}
  python2 setup.py build --use-system-cfitsio --use-system-erfa --use-system-expat # --use-system-wcslib
}

package_python-astropy() {
  depends=(python-scipy python-h5py cfitsio erfa)
  cd astropy-$pkgver

  install -d -m755 "$pkgdir"/usr/share/licenses/$pkgname/
  install -m644 -t "$pkgdir"/usr/share/licenses/$pkgname/ licenses/*
  python setup.py install --offline --root="$pkgdir" --prefix=/usr --optimize=1
}

package_python2-astropy() {
  depends=(python2-scipy python2-h5py cfitsio erfa)
  cd python2-astropy-$pkgver

  install -d -m755 "$pkgdir"/usr/share/licenses/$pkgname/
  install -m644 -t "$pkgdir"/usr/share/licenses/$pkgname/ licenses/*
  python2 setup.py install --offline --root="$pkgdir" --prefix=/usr --optimize=1

  for _bin in fitscheck fitsdiff fitsheader samp_hub volint wcslint fits2bitmap; do
    mv "$pkgdir"/usr/bin/${_bin}{,2}
  done
}
