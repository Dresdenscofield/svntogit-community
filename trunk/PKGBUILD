# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=dragonfly-reverb
pkgname=(dragonfly-reverb-clap dragonfly-reverb-jack dragonfly-reverb-lv2 dragonfly-reverb-vst dragonfly-reverb-vst3)
_name=DragonflyReverb
pkgver=3.2.7
pkgrel=1
pkgdesc="A concert hall reverb and a room reverb"
arch=(x86_64)
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=(GPL3)
groups=(lv2-plugins pro-audio vst-plugins)
depends=(gcc-libs glibc libx11 libxext)
makedepends=(freeverb3 gendesk jack libglvnd libsamplerate libx11 lv2)
checkdepends=(lv2lint)
options=(debug)
source=(https://github.com/michaelwillis/$pkgbase/releases/download/$pkgver/$pkgbase-source-v$pkgver.tar.gz)
sha512sums=('6c4a476d61009b557877e17c6f2acb81e80c3f666f4920e08ea653a4d26b58db21d651737fb9c23290ccb5adb415316f28bafbc614a1932632685734c7758d22')
b2sums=('dc0b911e9afb09c5d576fe4f3dd658b24026fd08290bf8efcc292761bf175aaebb8775fe9f8e0dac4614a42dd755c784b8966e420816ece119a4d51fe231e548')
_names=(DragonflyEarlyReflections DragonflyHallReverb DragonflyPlateReverb DragonflyRoomReverb)

prepare() {
  declare -A _description=(
    ["DragonflyEarlyReflections"]="A free stereo-to-stereo early reflections reverb effect"
    ["DragonflyHallReverb"]="A free stereo-to-stereo concert hall reverb effect"
    ["DragonflyPlateReverb"]="A free stereo-to-stereo plate reverb effect"
    ["DragonflyRoomReverb"]="A free stereo-to-stereo room reverb effect"
  )
  declare -A _generic=(
    ["DragonflyEarlyReflections"]="Early Reflections Reverb"
    ["DragonflyHallReverb"]="Concert Hall Reverb"
    ["DragonflyPlateReverb"]="Plate Reverb"
    ["DragonflyRoomReverb"]="Room Reverb"
  )

  cd $_name-Source-v$pkgver

  # explicitly remove vendored freeverb as it causes issues: https://github.com/michaelwillis/dragonfly-reverb/issues/129
  rm -frv common/freeverb/

  for name in "${_names[@]}"; do
    gendesk -n \
            --pkgname io.github.michaelwillis.$name \
            --name $name \
            --exec $name \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  export SYSTEM_FREEVERB3="true"
  make -C $_name-Source-v$pkgver
}

check() {
  local _plugin_name=""
  declare -A _urls=(
    ["DragonflyHallReverb"]='https://github.com/michaelwillis/dragonfly-reverb'
    ["DragonflyEarlyReflections"]='urn:dragonfly:early'
    ["DragonflyPlateReverb"]='urn:dragonfly:plate'
    ["DragonflyRoomReverb"]='urn:dragonfly:room'
  )

  for _plugin_name in "${_names[@]}"; do
    lv2lint -Mpack -s "lv2_generate_ttl" -I $_name-Source-v$pkgver/bin/$_plugin_name.lv2/ "${_urls[$_plugin_name]}"
  done
}

package_dragonfly-reverb-clap() {
  pkgdesc+=" - CLAP plugin"
  depends+=(clap-host libGL.so libfreeverb3.so)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name.clap -t "$pkgdir/usr/lib/clap/"
  done
}

package_dragonfly-reverb-jack() {
  pkgdesc+=" - JACK standalone"
  depends+=(jack libGL.so libfreeverb3.so)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name -t "$pkgdir/usr/bin"
  done
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

package_dragonfly-reverb-lv2() {
  pkgdesc+=" - LV2 plugin"
  depends+=(libGL.so libfreeverb3.so lv2-host)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name.lv2/*.so -t "$pkgdir/usr/lib/lv2/$name.lv2/"
    install -vDm 644 bin/$name.lv2/*.ttl -t "$pkgdir/usr/lib/lv2/$name.lv2/"
  done
}

package_dragonfly-reverb-vst() {
  pkgdesc+=" - VST plugin"
  depends+=(libGL.so libfreeverb3.so vst-host)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name-vst.so -t "$pkgdir/usr/lib/vst"
  done
}

package_dragonfly-reverb-vst3() {
  pkgdesc+=" - VST3 plugin"
  depends+=(libGL.so libfreeverb3.so vst3-host)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name.vst3/Contents/$CARCH-linux/$name.so -t "$pkgdir/usr/lib/vst3/"
  done
}
