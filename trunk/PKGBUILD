# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter_console
pkgver=6.4.4
pkgrel=2
pkgdesc="An IPython-like terminal frontend for Jupyter kernels in any language."
arch=('any')
url="https://pypi.org/project/jupyter_console/"
license=('BSD')
depends=('ipython' 'python-jupyter_client' 'python-ipykernel'
         'python-pygments' 'python-prompt_toolkit')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
#source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_console/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/j/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('172f5335e31d600df61613a97b7f0352f2c8250bbd1092ef2d658f77249f89fb')

build() {
  cd jupyter_console-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd jupyter_console-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
