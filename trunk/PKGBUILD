# $Id$
#Maintainer: Biru Ionut <biru.ionut at gmail.com>
#Contributor: Hugo Doria <hugo@archlinux.org>
#Contributor: Sergej Chodarev <sergejx@centrum.sk>
#Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=2.28.0
pkgrel=1
pkgdesc="Set of plugins for gedit"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GeditPlugins"
depends=('gedit>=2.28.0' 'vte' 'gucharmap' 'gnome-python')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.18' 'gnome-common>=2.28.0')
options=('!libtool' '!emptydirs')
install=gedit-plugins.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/gedit-plugins/2.28/$pkgname-$pkgver.tar.bz2)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-plugins=all || return 1

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install || return 1

  install -m755 -d $pkgdir/usr/share/gconf/schemas
  gconf-merge-schema $pkgdir/usr/share/gconf/schemas/${pkgname}.schemas $pkgdir/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
md5sums=('7a0068ef250f2cfe8d60a5cf0e7a7c93')
