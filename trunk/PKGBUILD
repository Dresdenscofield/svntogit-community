# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=5.10.33
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL3')
depends=('deepin-qt5integration' 'enca' 'kcodecs' 'polkit-qt5' 'syntax-highlighting' 'libchardet' 'uchardet')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-editor/archive/$pkgver.tar.gz"
        fix-include.patch)
sha512sums=('cef3ec65a1ee83bcd81e574646256d216e9a420fa83cbf9a2597e09e1781af094999714674b093bd75dae389b884226ef1f702812d2f495633f2de93058cf41e'
            '95809c705ab47c5c670a32605b815742225929db49cf0a62ff2cf82c1ff6ffeb190731fbee2a8e7002457fe072efc9fff741309655b7bfeeb3d06d240aa66750')

prepare() {
  cd deepin-editor-$pkgver
  patch -p1 -i ../fix-include.patch
}

build() {
  cd deepin-editor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  ninja
}

check() {
  cd deepin-editor-$pkgver
  ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
