# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-latex2mathml
pkgver=3.63.3
pkgrel=1
pkgdesc="Pure Python library for LaTeX to MathML conversion"
url="https://github.com/roniemartinez/latex2mathml"
license=('MIT')
arch=('any')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-multidict' 'python-xmljson')
source=("https://github.com/roniemartinez/latex2mathml/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('83c0f356001fa7c6a1c4e63c0e2b82e4ab57606735cf73f375a62a796871f8829512affc5785255e3dcdc4e913400f46501ae0731ec1f0b27b8200874c71fdc3')

prepare() {
  cd latex2mathml-$pkgver
  sed -i 's/--cov=latex2mathml --cov-report term-missing --cov-report=xml --cov-report=html//' pyproject.toml
}

build() {
  cd latex2mathml-$pkgver
  python setup.py build
}

check() {
  cd latex2mathml-$pkgver
  pytest
}

package() {
  cd latex2mathml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
