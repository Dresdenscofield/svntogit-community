# Maintainer: David Runge <dvzrv@archlinux.org>

_name=unearth
pkgname=python-unearth
pkgver=0.5.2
pkgrel=1
pkgdesc="A utility to fetch and download python packages"
arch=(any)
url="https://github.com/frostming/unearth"
license=(MIT)
depends=(python-packaging python-requests)
makedepends=(python-build python-installer python-pdm-pep517 python-wheel)
checkdepends=(python-flask python-pytest python-requests-wsgi-adapter)
optdepends=('python-keyring: use keyring for authentication')
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
)
sha256sums=('e51696b1913f591b41927b3eef8cebe84668fb769bf81f88094b7cc99afa706d')
b2sums=('c14626c49c83c69ae31c3bd7626b412afd74b8dcafbfad460b6d30b918e4f48864fa2c7c8f6fe85063baedd488f69137d3d1386502bd53118bd905bfc003ad60')

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
