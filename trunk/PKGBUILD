# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyphen
pkgname=python-pyphen
pkgver=0.13.1
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
license=(GPL2 LGPL2.1 MPL)
depends=(python)
makedepends=(python-build python-flit-core python-installer python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('08297f14e7f2d35d50f4bab6af727b46637a180adfae9ff6fcf20dbd4771aa39ca64bcc1bcc5745e395157dffe8383e107d4f4541fa6f9995d317fc62ff85b67')
b2sums=('e2c86dcc0eabb8d05b262af08b207b72e5e61dccf98d3099bdcf4d243d17406d1abd2d2cd2c3071bd301ae3ebb73cf034f7bbd2f58b9b921075e5087d7696ca2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v -c /dev/null tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
