# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-classy-tags
pkgname=python-django-classy-tags
pkgver=3.0.1
pkgrel=2
pkgdesc="Class based template tags for django"
arch=(any)
url="https://github.com/django-cms/django-classy-tags"
license=(BSD)
depends=(python-django)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-django4.1.patch
)
sha512sums=('3d207cb398c44d058fbcc9d30dddf7cef22938b0731409fafea8776e7c5b7e59e3c8740e119a8fb6e46e5f7d2d64dd54afbdf57755c2f4c03457c6b47a0890aa'
            '1e2d4bfdbdb46c1f4efa1ff5059fa00cceadb96f730a04b19de5d27be3cb4b509a5790580284a71912c128b91501241ea9887e307c8921d9ffe1650a84a985e4')
b2sums=('67fed2308a6b50bfd0cecdf60dca7680f69cc7cfe201eceed32500b7964f3a537c11e5e676d9dbfe11535f8bde7d06bead2da9f0ef2a6e3a04783619955d233b'
        '97df3d5bbefc8464e59bc6ff960a6a622315710e884eb4fe831d22686b0dba8855f3e41d543759967152843551bed32662c0818c3cfc1c09718314d379494e24')

prepare() {
  # compatibility with django >= 4.1: https://github.com/django-cms/django-classy-tags/issues/96
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-django4.1.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="$PWD/build/lib:$PYTHONPATH"
  python tests/settings.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
