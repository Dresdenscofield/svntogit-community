# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=psalm
pkgver=4.27.0
pkgrel=2
_commit=faf106e717c37b8c81721845dba9de3d8deed8ff
pkgdesc='A static analysis tool for finding errors in PHP applications'
url="https://psalm.dev/"
arch=('any')
license=('MIT')
depends=('php')
makedepends=('composer' 'php7' 'git')
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vimeo/psalm/archive/refs/tags/${pkgver}.tar.gz"
source=("git+https://github.com/vimeo/psalm.git#commit=${_commit}"
        "composer.lock")
b2sums=('SKIP'
        '10448d517e7de4158980e7c85c001312d18b63505eee70888dc894c7e8732d903fd98ff406a8b22c46794385c0da6cc68e8de44bf258b3264a35ab7bec1be23d')

prepare() {
  cd ${pkgname}
  cp ../composer.lock .
  composer install
  php7 /usr/bin/composer bin box install
  php bin/improve_class_alias.php
}

build() {
  cd ${pkgname}
  vendor/bin/box compile
}

package() {
  cd ${pkgname}
  install -Dm 755 ./build/psalm.phar "${pkgdir}/usr/bin/psalm"
}

# vim: ts=2 sw=2 et:
