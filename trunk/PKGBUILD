# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.17.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux python-typing-extensions)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('4de874d832027ac79b6aacf687dd36df8bcb5ea56e06c742cb33cac508a29aa73c1ea96d302d4f407cd73f83cb3b3b3296757b94555cc57d6dfa2ab7ab97bd4e')
b2sums=('6a226a91ced3b0a475f589b6b079ee9387e4bc27d7c5da84abfdfb13817f1d2da29e2b7a6e38d949ee3e62fa6733d244c1c076a7fab9b29a8babb2a31c4ebf6f')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/788
    --deselect tests/test_workspacebuilder.py::test_automatic_rename_option
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv "${_deselected[@]}" tests
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
