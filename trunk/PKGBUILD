# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.1.2
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua)
makedepends=(cmake flac jq libid3tag openssl pcre2 perl)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/jcorporation/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('de75c020932670c21a3a23ef3eace4bee3178d3e04a6c13edc7bbd32b372b78c5706d0b0307d8ef4d30ee73351857231bf1f333489a119e643347774756f70e0')
b2sums=('59c5d9c7e5925bd562929855e1870aa41bc1c5ddf7c8ef58e88abebceb56e779e8c16316bbe048b25ee6b0319f5ae95e9eb0513608508b1adb078c63ee91673b')

build() {
  local cmake_options=(
    -B build
    -S $_name-$pkgver
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
