# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>
pkgname=xiphos
pkgver=3.1.4
pkgrel=9
pkgdesc="Bible study tool for GTK+"
arch=('x86_64' 'i686')
url="http://xiphos.org/"
license=('GPL')
depends=('libgsf' 'libglade' 'gtkhtml' 'sword')
makedepends=('gnome-doc-utils' 'intltool' 'python2')
conflicts=('gnomesword')
provides=('gnomesword')
replaces=('gnomesword')
install=xiphos.install
source=("http://downloads.sourceforge.net/gnomesword/$pkgname-$pkgver.tar.gz")
md5sums=('4d6f7805c4c8185f5c4ac3bd95fc705a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '0,/on/s//on2/' waf
  ./waf configure --prefix=/usr --enable-gtkhtml
  ./waf build --prefix=/usr 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 ./waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
