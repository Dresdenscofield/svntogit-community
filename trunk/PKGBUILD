# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=37.2
pkgrel=2
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz"
        $pkgname-capability.patch::https://github.com/pymumu/smartdns/commit/1640e9e6a161bcdd9c3fd704f9047dcf3b259aee.patch)
sha512sums=('16276cc2d1922e6f8657aa86a7bfe6b05d35cd0663bb6934620b5cc45db614594341cc6fa3acfa7d3d59fdd262b56f3bdc0839d97ffde6540c05c54d5c7a40d8'
            'd357532ccef3a0686eac483f4d609cca4ea7a25d399c883d6864d2dea8f12c6afeb795e2d39e9bfff5d54d97882428f381893b68a8d8e885ab27ae01d3b685ad')

prepare() {
  patch -d smartdns-Release$pkgver -p1 < $pkgname-capability.patch
}

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  make DESTDIR="$pkgdir" RUNSTATEDIR=/run SBINDIR=/usr/bin install

  rm -r "$pkgdir"/etc/init.d
}
