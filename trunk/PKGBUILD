# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Miles McLean <mills00013@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Brad Pitcher <bradpitcher@gmail.com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgbase=networkmanager-l2tp
pkgname=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
pkgver=1.20.4
_pppver=2.4.9
pkgrel=3
pkgdesc='L2TP support for NetworkManager'
arch=(x86_64)
url='https://github.com/nm-l2tp/NetworkManager-l2tp'
license=(GPL2)
provides=(networkmanager-l2tp)
replaces=(networkmanager-l2tp)
conflicts=(networkmanager-l2tp)
depends=(libnma ppp=$_pppver xl2tpd)
makedepends=(gtk3 gtk4 intltool libnma-gtk4 python)
optdepends=('strongswan: IPSec support')
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
b2sums=('ccd4f0a15b9b47635d3f1798ef1b6778657c269c2499b630b35261be6279d6bbffddcd0bc4c6cf17f5d929c88e356376f143227dc3ae1f3c138a7f166e5b46bf')

prepare() {
  ln -sf NetworkManager-l2tp-$pkgver $pkgbase
  cp -r NetworkManager-l2tp-$pkgver $pkgbase-gtk4
}

build() {
  export NOCONFIGURE=1
  cd $pkgbase
  ./autogen.sh
  ./configure \
    --libexecdir=/usr/lib/NetworkManager \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --without-gtk4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  cd ../$pkgbase-gtk4
  ./autogen.sh
  ./configure \
    --libexecdir=/usr/lib/NetworkManager \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-gtk4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_networkmanager-l2tp-gtk3() {
  conflicts+=(networkmanager-l2tp-gtk4)
  make -C $pkgbase DESTDIR="$pkgdir" install
  install -Dm644 $pkgbase/NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}

package_networkmanager-l2tp-gtk4() {
  conflicts+=(networkmanager-l2tp-gtk3)
  depends+=(gtk4 libnma-gtk4)
  make -C $pkgbase-gtk4 DESTDIR="$pkgdir" install
  install -Dm644 $pkgbase-gtk4/NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}
