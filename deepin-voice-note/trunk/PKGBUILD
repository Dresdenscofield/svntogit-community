# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.10.18
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'qt5-webengine' 'vlc')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-flags.patch::https://github.com/linuxdeepin/deepin-voice-note/pull/54.patch)
sha512sums=('3a9fc6301fe56106a0b28ba4beb5b6e6873b4e8d03ea2d0f970917614ba27b5eb8981256eb60bab0792db0de5778577ac90056c8a596afea41bf62801b644940'
            'a5fd4798c9b0d264fc33f0da99e0c5990b524e6c6ec555be1705c78cae2f45b0e3f342f95c846d9a978449e31d39d53027edde0c930355ce22d32ba8b5a1e11f')

prepare() {
  cd deepin-voice-note-$pkgver
  patch -p1 -i ../$pkgname-fix-flags.patch
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-voice-note-$pkgver
  ninja test || :
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
