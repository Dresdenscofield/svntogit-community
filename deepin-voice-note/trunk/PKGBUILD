# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.10.16
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'qt5-webengine' 'vlc')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-flags.patch::https://github.com/linuxdeepin/deepin-voice-note/pull/54.patch)
sha512sums=('c92b23da7873714ae39126883547dca7140661290d5dd45347c95712de80a99966bf8d896dbcbbfc844cf1db8e5f8684f2df8cedf177ac3e347c3e0d25a6ecd0'
            '2ba9f4774a6485eb563dfe3b5f9df9d58e4c6208b752459a31b9979f3e2c2d4a6f551c244a13983873490358520c41ec331c54ba1e46d65f162f49efb86914e8')

prepare() {
  cd deepin-voice-note-$pkgver
  patch -p1 -i ../$pkgname-fix-flags.patch
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-voice-note-$pkgver
  ninja test || :
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
