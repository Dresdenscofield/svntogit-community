# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-rubytest
pkgver=0.7.0
pkgrel=1
pkgdesc='Core API for Ruby Universal Test Harness'
arch=(any)
url='https://github.com/rubyworks/rubytest'
license=(BSD)
depends=(ruby)
checkdepends=(ruby-ae ruby-qed)
options=(!emptydirs)
source=(https://github.com/rubyworks/rubytest/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('17127f1e80f58bc6538e193453c0e618f39b4c0929aef283eaabacfb6410c794')

build() {
  local _gemdir="$(gem env gemdir)"
  cd rubytest-$pkgver
  gem build .gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    rubytest-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd rubytest-$pkgver
  GEM_HOME="tmp_install/$_gemdir" qed -Ilib
}

package() {
  cd rubytest-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
