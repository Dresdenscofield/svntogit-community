# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.7.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=(x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL2)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(alsa-lib cmake doxygen docbook-xsl fluidsynth graphviz
libpulse qt5-tools sonivox)
provides=(libdrumstick-alsa.so libdrumstick-file.so libdrumstick-rt.so
libdrumstick-widgets.so)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('3199a0a70afa3a9eca0aaf5acdd2dc53d4d7fc0aee69736102b9d8f20a9e586120188515ef2da112e187532c6d0cbcb68f5ae6c44f4308bb24b03abdce89f30a')
b2sums=('8ad7bed7d060af3910a40c661efb19ff729eb55dc37fed27c1c38fd2311b74f2e62280299333d37ea177b0e23f8691a98c03f2f698f9cdd3db56e4180a8e55a1')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(libasound.so libfluidsynth.so libpulse.so libpulse-simple.so libsonivox.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
