# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.17.2
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
options=('debug' !lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        Cargo.lock)
sha256sums=('6889af7fde60ad00f1702808c8d57b603c8b8be3cc9f64e8c2e8206cb0742c4c'
            '248cef40628df9d46537eb2217b18bd3af7fc97d6e83a7899be1bd567e148e5a')
b2sums=('41385df36e680e4643ffacc80346b5a4709409779bf282e6fb16dcb005a6d2525dae9734f77260a4f52d6c64a24da6750074628eda643a59fefe82024774f4e9'
        '22f33ad346724672bbb78a7817197b204fe7d7a2af72e051dd3f4317e4254099b0bfc122dd731729dd4d4aa357a362088683c765cc9b28923b7bb0ebbfeaa135')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cp ../../Cargo.lock ..
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
