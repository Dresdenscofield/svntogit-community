# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=modern-uri
pkgname=haskell-modern-uri
pkgver=0.3.5.0
pkgrel=1
pkgdesc="Modern library for working with URIs"
url="https://github.com/mrkkrp/modern-uri"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-contravariant' 'haskell-hashable'
         'haskell-megaparsec' 'haskell-profunctors' 'haskell-reflection' 'haskell-tagged')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-megaparsec' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('34bddb17502c5ff30c256917127573f44858f04f260aa91935eddefcce81136efc25679b0ea6f00e2a653e1bc93b884baf7550091ecee06bb0ae082d1adc9113')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
