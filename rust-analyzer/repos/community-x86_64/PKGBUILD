# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2022-11-14
pkgver=${_pkgver//-}
pkgrel=3
pkgdesc='Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://rust-analyzer.github.io/
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust-src')
makedepends=('git')
source=("git+https://github.com/rust-lang/$pkgname.git#tag=$_pkgver"
        'rust-analyzer-support_standalone_server_in_lib.patch')
b2sums=('SKIP'
        '48ddf95953a94fb701b240903fb8e6ebb275933fe1e3f023cab06b10abd3a8d307babfb172ea8b93f7fbebf3e61d2fe1c4bb45ca66e406dd823c4895d515432b')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  patch --forward --strip=1 --input=../rust-analyzer-support_standalone_server_in_lib.patch
}

build() {
  cd $pkgname
  CFG_RELEASE=1 cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname{,-proc-macro-srv}
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
