# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=python-pycodestyle
pkgver=2.9.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://pycodestyle.pycqa.org"
license=('custom:Expat')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/PyCQA/pycodestyle/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1550dfb353ecbe2e01c06f2865c7fb56c03f422a5fb31fdca468fb3d651122915bed3a36dce88a81ce585a32015cc158953d0e453bac9a0a4193096aa7c6a809')

build() {
  cd pycodestyle-$pkgver
  python setup.py build
}

check() {
  cd pycodestyle-$pkgver
  python -m unittest discover
}

package() {
  cd pycodestyle-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
