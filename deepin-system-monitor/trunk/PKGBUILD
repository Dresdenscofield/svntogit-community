# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.9.27
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-dock' 'dtkwm' 'procps-ng' 'libcap')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('ba62675be1aae65a9559318bc2e2310c06f1fab3707c30bb8d26a1bfb512b76cd6d57510b31fda7741c0bb17ef38577a0683072982aff0fbb89e452c1b21e57c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../deepin-system-monitor-$pkgver -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DUSE_DEEPIN_WAYLAND=OFF
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
