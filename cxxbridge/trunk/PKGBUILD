# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.81
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('8955f628050e080ba8c05974de812f91be6b93d7bcb4640717658939ab409b84f9d65b44a40dba62e20a38b0c788c1b62e239b9aecdf0139458e9889c9ec3ac4'
            'ae612adef6e5bd64aef006fa57d02ff1639df12dff690a43831d160562d406a7a1e74bea478462d97e5b1d9a4d6eea66abbc93c23f684978dba81a0702644379')
b2sums=('2e57d255bed4e19963cbc38e7e8606bd405877d657462b0ca807e826453c1e8514fd4eff7f47cae819ab5f74a8e1eb1a8c0c5ea9c46b02715b71651c6e4dde64'
        'd4afb6054748ddb2f3409ee07613e1e2bd6e5e0454bb7afd0aaa418de40e8ca02d0832054290780a7b376835e2f6d277300caaf1a490b557f4ef43375ab34565')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
