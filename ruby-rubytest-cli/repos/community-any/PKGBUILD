# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-rubytest-cli
pkgver=0.1.0
pkgrel=1
pkgdesc='Rubytest CLI is a command-line interface for running tests for Rubytest-based test frameworks'
arch=(any)
url='https://github.com/rubyworks/rubytest-cli'
license=(BSD)
depends=(ruby-ansi ruby-rubytest)
options=(!emptydirs)
source=(https://github.com/rubyworks/rubytest-cli/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('6ddfc284488d70f5aeefcded06880c963704a9affc3a5e9ffa968d03ce54fa94')

build() {
  local _gemdir="$(gem env gemdir)"
  cd rubytest-cli-$pkgver
  gem build .gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    rubytest-cli-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

package() {
  cd rubytest-cli-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
