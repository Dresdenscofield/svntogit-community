# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=(php-igbinary php7-igbinary)
pkgver=3.2.11
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=(x86_64)
url="https://github.com/igbinary/igbinary"
license=(BSD)
depends=(glibc)
makedepends=(php7 php)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$_name/$_name/archive/$pkgver.tar.gz)
sha512sums=('f9e337e08cc10c8bd3bcdb6f688bc952853258b1dacf1142f2203336d5ae31ce1966b107ea621d0bda0f4ec68baccf00e7d61c793d1a83a75a222c3b1892ff31')
b2sums=('e91b0d25f1724c18827453b2f409822b6d5174a06de8a78d96bb30037b0f0c87cf89208bccc3df120b75ad67aaef766ac176e0fb2acad5cb54dbcc26df200e49')

prepare() {
  mv -v $_name-$pkgver $pkgbase-$pkgver
  # disable the extension by default
  sed 's/extension/;extension/g' -i $pkgbase-$pkgver/$_name.php.ini

  cp -av $pkgbase-$pkgver ${pkgname[1]}-$pkgver
  (
    cd $pkgbase-$pkgver
    phpize
  )
  (
    cd ${pkgname[1]}-$pkgver
    phpize7
  )
}

build() {
  (
    cd $pkgbase-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd ${pkgname[1]}-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    NO_INTERACTION=1 make -k test -C $pkgbase-$pkgver
  )
  (
    NO_INTERACTION=1 make -k test -C ${pkgname[1]}-$pkgver
  )
}

package_php-igbinary() {
  local _interpreter_version="$(php --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php)
  _provides=(php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 $_name.php.ini "$pkgdir/etc/php/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_php7-igbinary() {
  local _interpreter_version="$(php7 --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php7)
  _provides=(php-igbinary=$pkgver php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php7/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 "$_name.php.ini" "$pkgdir/etc/php7/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
