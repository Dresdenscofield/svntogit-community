# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Nelson Menon <nelsonmenon@gmail.com insanatorium.wordpress.com>

pkgname=freecol
pkgver=0.13.0
pkgrel=2
pkgdesc="A turn-based strategy game based on Colonization"
arch=('any')
url="http://www.freecol.org/"
license=('GPL')
depends=('java-runtime>=11')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.zip
        fc.desktop
        fc.sh)
sha512sums=('b0f365ef4e35feae50b477b173951124b6a68773d910b169a2e0ae5d63f52f93a4105c54fa29f036057121ffc14a0ea44c6d3f6550890998bbcb586b984135e3'
            '9abd209dfbfdf87c5d90145493f021df4c5b80cf30f6b224116242adf778518907c149a3bb3e1288e8afb0447261f348cc228bdc6eca525ea6e55203b911c453'
            '27386ad535e0cbfc77f5620e0d7d1f435eb4e1dbb9695474d482b24339bb884cc47bace6347dae241f6d4d9083fe6643ebb209fc3aacf4edd9fe56a995c67907')

package() {
  cd "$srcdir/$pkgname"

  install -Dm644 FreeCol.jar "$pkgdir/usr/share/java/$pkgname/FreeCol.jar"
  install -Dm644 FreeCol.pdf "$pkgdir/usr/share/doc/$pkgname/FreeCol.pdf"
  install -Dm644 $pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 ../fc.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm755 ../fc.sh "$pkgdir/usr/bin/$pkgname"

  cp -r data jars "$pkgdir/usr/share/java/$pkgname/"
  find "$pkgdir/usr/share/java/$pkgname" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/usr/share/java/$pkgname" -type f -exec chmod 644 '{}' \;
}
