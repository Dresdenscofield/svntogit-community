# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=goaurrpc
pkgver=1.1.2
pkgrel=1
license=('MIT')
pkgdesc="An implementation of the aurweb /rpc service in go"
depends=('glibc')
makedepends=('go')
arch=('x86_64')
url="https://github.com/moson-mo/goaurrpc"
source=("${pkgname}-${pkgver}::https://github.com/moson-mo/goaurrpc/archive/refs/tags/v${pkgver}.tar.gz"
        goaurrpc-local.sample
        goaurrpc-remote.sample
        goaurrpc.service
        goaurrpc.sysusers)
sha256sums=('db6338c2d2785918731629467d0a7de76ce73c8a121cb705e34958c8e3bd7f3a'
            '3a2921bed5695e9c0ebb957cd42630047fbce71af7876b449016a82473a1a89d'
            'fcea00cbaee4d3e336cfae8faeca589b044d9b82c486ba012d9a9fc3b949039e'
            'e6a298e647fcf3b255d9886180b992f0dc447e763509c50513473f6d9b9ac5e3'
            '45ac878e62e8e43c55645ebbd71fc8e10ce32c81d1a769979f48ac5f25392ff3')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p dist/
}

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o dist/goaurrpc \
    .
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 dist/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 "${srcdir}"/${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -vDm 644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "${srcdir}"/goaurrpc-local.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 "${srcdir}"/goaurrpc-remote.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
