# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=goaurrpc
pkgver=1.1.1
pkgrel=1
license=('MIT')
pkgdesc="An implementation of the aurweb /rpc service in go"
depends=('glibc')
makedepends=('go')
arch=('x86_64')
url="https://github.com/moson-mo/goaurrpc"
source=("${pkgname}-${pkgver}::https://github.com/moson-mo/goaurrpc/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('98a0fbe2f64d552a265adcb80c94809c93e75f65f9b12e03d3e6df5427c237f0')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p dist/
}

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o dist/goaurrpc \
    .
}

check() {
  cd ${pkgname}-${pkgver}
  go test ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 dist/${pkgname} "${pkgdir}/usr/bin/$pkgname"
  install -D -m644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
