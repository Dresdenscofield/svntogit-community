# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=goaurrpc
pkgver=1.1.7
pkgrel=1
license=('MIT')
pkgdesc="An implementation of the aurweb /rpc service in go"
depends=('glibc')
makedepends=('go' 'git')
arch=('x86_64')
url="https://github.com/moson-mo/goaurrpc"
source=("git+https://github.com/moson-mo/goaurrpc.git#tag=v${pkgver}?signed"
        goaurrpc-local.sample
        goaurrpc-remote.sample
        goaurrpc.service
        goaurrpc.sysusers)
sha256sums=('SKIP'
            '3a2921bed5695e9c0ebb957cd42630047fbce71af7876b449016a82473a1a89d'
            'fcea00cbaee4d3e336cfae8faeca589b044d9b82c486ba012d9a9fc3b949039e'
            'e6a298e647fcf3b255d9886180b992f0dc447e763509c50513473f6d9b9ac5e3'
            '45ac878e62e8e43c55645ebbd71fc8e10ce32c81d1a769979f48ac5f25392ff3')
validpgpkeys=(
  D5AD89388A7C6C9C22E790994A4760AB4EE15296  # Mario Oenning <moson-mo@users.noreply.github.com>, retrieved from https://github.com/moson-mo.gpg
  )

prepare() {
  cd ${pkgname}
  mkdir -p dist/
}

build() {
  cd ${pkgname}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o dist/goaurrpc \
    .
}

check() {
  cd ${pkgname}
  go test ./...
}

package() {
  cd ${pkgname}
  install -vDm 755 dist/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 "${srcdir}"/${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -vDm 644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "${srcdir}"/goaurrpc-local.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 "${srcdir}"/goaurrpc-remote.sample -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
