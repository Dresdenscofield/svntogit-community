# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=3.6.1
pkgrel=3
epoch=1
pkgdesc='Desktop Planetarium'
url='https://kde.org/applications/education/kstars/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(stellarsolver libraw kplotting knewstuff knotifyconfig qt5-datavis3d qt5-quickcontrols qt5-websockets qtkeychain
         libindi breeze-icons hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen)
optdepends=('xplanet: XPlanet support')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/education/kstars/-/commit/87317b63.patch)
sha256sums=('5966ed7eabc6775dfe6b8d73f3f32395c865969ba12d7d3c9eed793a56147ae2'
            'SKIP'
            'f3fc1ab712d12e7adcb609d98b3ca11147ed7a839d817a637aa587b4188a1611')
validpgpkeys=(259D9FCEE9175351965633696D9CE2AEE028C4F3) # Jasem Mutlaq <mutlaqja@ikarustech.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 87317b63.patch # Fix build with libindi 1.9.9
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
