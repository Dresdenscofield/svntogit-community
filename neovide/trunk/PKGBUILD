# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: peeweep <peeweep at 0x0 dot ee>

pkgname=neovide
pkgver=0.10.0
pkgrel=1
pkgdesc='No Nonsense Neovim Client in Rust'
arch=(x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(fontconfig
         freetype2
         libglvnd
         neovim
         sndio)
makedepends=(cargo
             cmake
             gtk3
             make
             python
             sdl2)
optdepends=('vulkan-intel: vulkan support for intel')
_archive=("$pkgname-$pkgver")
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('b252d2fd6ca6acc0cefdc522f9400076ec80203a9429aa38289fe1a4520abe64')

prepare() {
	cd "$_archive"
	sed -i \
		-e '/^incremental/a opt-level = 3' \
		Cargo.toml
	# v0.10.0 lockfile out of date, https://github.com/neovide/neovide/issues/1482
	cargo update
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	cargo build --frozen --release --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/applications/" "assets/$pkgname.desktop"
	for px in 16 32 48 256; do
		install -Dm0644 "assets/$pkgname-${px}x${px}.png" \
			"$pkgdir/usr/share/icons/hicolor/${px}x${px}/apps/$pkgname.png"
	done
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
