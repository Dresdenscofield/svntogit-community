# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-argh
pkgver=0.26.2
pkgrel=10
pkgdesc="An unobtrusive argparse wrapper with natural syntax"
arch=(any)
url="https://pypi.python.org/pypi/argh"
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-iocapture')
source=("https://github.com/neithere/argh/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('eb5476ac788e5c4f5e01825455630218aba055c29240637988f4c99532e7bdb6')

build() {
  cd argh-$pkgver
  python setup.py build
}

check() {
  cd argh-$pkgver
  # https://github.com/neithere/argh/issues/148#issuecomment-981508602
  pytest --deselect test/test_integration.py::test_invalid_choice \
         --deselect test/test_integration.py::test_explicit_cmd_name
}

package() {
  cd argh-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
